@WINDY_PANCHIRA

;デフォルトの確率は25%
LOCAL = 25

;時止め中だと発生しない
SIF FLAG:70
	RETURN 0

;あなた一人だけだと発生しない
SIF TARGET <= 0
	RETURN 0

;対象が睡眠中だと発生しない
SIF CFLAG:睡眠
	RETURN 0

;屋外（屋根の有無は問わない）でないと発生しない
SIF !OUTROOF(CFLAG:MASTER:現在位置) && INROOM(CFLAG:MASTER:現在位置)
	RETURN 0

;地底以外だと晴れか曇りでないと発生しない
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && !(TIME:5 >= 0 && TIME:5 <= 3) 
	RETURN 0

;地底以外で台風、および吹雪だと発生しない
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && GROUPMATCH(FLAG:異常気象, 9, 10)
	RETURN 0

;会話、仕事を手伝う、自由行動に付き合う、休憩、掃除、戦闘訓練、待つのいずれかでないと発生しない
SIF !(SELECTCOM == 300 || SELECTCOM == 304 || SELECTCOM == 368 || SELECTCOM == 403 || SELECTCOM == 410 || SELECTCOM == 411 || SELECTCOM == 440)
	RETURN 0

;一部の自由行動では発生しない
IF SELECTCOM == 368
	SELECTCASE Activity_Type:TARGET
	CASE 2,3,7,11,13,15,16,18
		RETURN 0
	ENDSELECT
ENDIF

;一度発生したら5ターンのクールタイムあり
SIF FLAG:風チラCT > 0
	RETURN 0

;暴風以上だとパンツどころではないので発生しない
SIF WIND_VELOCITY >= 2
	RETURN 0

;妖怪の山（山頂）だと発生確率上昇
SIF GET_MAPID(CFLAG:MASTER:現在位置) == 8
	LOCAL += 10

;地底以外で強風だと発生確率上昇
SIF WIND_VELOCITY == 1 && GET_MAPID(CFLAG:MASTER:現在位置) != 9
	LOCAL += 10

$LOOP
IF RAND:2 == 0
	SIF EQUIP:TARGET:裤子
		RETURN 0
	SIF !(EQUIP:TARGET:裙子 || EQUIP:TARGET:連衣裙 || EQUIP:TARGET:和服) || EQUIP:TARGET:0 == 0
		RETURN 0
	;着物だと発生確率低下
	SIF !(EQUIP:TARGET:裙子 || EQUIP:TARGET:連衣裙) && EQUIP:TARGET:和服
		LOCAL -= 10
	;デート中だと発生確率低下
	SIF CHK_DATENOW(CFLAG:TARGET:约会中)
		LOCAL -= 10
	IF RAND:100 < LOCAL + CFLAG:TARGET:めくれ補正
		CALL PANCHIRA
	ELSE
		RETURN 0		
	ENDIF
ELSE
	SIF EQUIP:MASTER:裤子
		RETURN 0
	SIF !(EQUIP:MASTER:裙子 || EQUIP:MASTER:連衣裙 || EQUIP:MASTER:和服) || EQUIP:MASTER:0 == 0
		RETURN 0
	SIF !(EQUIP:MASTER:裙子 || EQUIP:MASTER:連衣裙) && EQUIP:MASTER:和服
		LOCAL -= 10
	SIF CHK_DATENOW(CFLAG:TARGET:约会中)
		LOCAL -= 10
	IF RAND:100 < LOCAL
		CALL REV_PANCHIRA
	ELSE
		RETURN 0		
	ENDIF
ENDIF
RETURN 1

@PANCHIRA
DRAWLINE
IF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
    PRINTFORM 突然,狂暴的强风变得更加猛烈,一时疏忽的%CALLNAME:TARGET%的
ELSE
    PRINTFORM 突然,一阵风卷起,没有防备的%CALLNAME:TARGET%的 
ENDIF
IF EQUIP:TARGET:裙子
	PRINTFORM %SHOW_BOTTOM(TARGET,1)%
ELSEIF EQUIP:TARGET:和服
	PRINTFORM %SHOW_TOP(TARGET,1)%的下摆
ELSEIF EQUIP:TARGET:連衣裙
	PRINTFORM %SHOW_TOP(TARGET,2)%的下摆
ENDIF
PRINTFORM 被
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
    PRINTFORM 猛烈地
PRINTFORML 翻起来了
 
PRINTFORM %CALLNAME:TARGET%

SELECTCASE SELECTCOM

CASE 300
    PRINTFORM 和在附近的
CASE 304
    IF CFLAG:TARGET:350 == 46
        PRINTFORM 一起玩耍的
    ELSE
        PRINTFORM 正在帮助工作的
    ENDIF
CASE 368
    SELECTCASE Activity_Type:TARGET
    CASE 0
        PRINTFORM 正在询问在考虑事情的
    CASE 1
        PRINTFORM 和一起玩耍的
    CASE 4
        PRINTFORM 正在帮忙打扫的
    CASE 5
        PRINTFORM 和一起运动的
    CASE 6
        PRINTFORM 正在询问正在读书的
	CASE 8
        IF TFLAG:193 == -2
            SELECTCASE TALENT:TARGET:音楽知識
            CASE 1
                PRINTFORM 正在听演奏的
            CASE 2
                PRINTFORM 正在听歌唱的
            CASE 3
                PRINTFORM 正在观看舞蹈的
            ENDSELECT
        ELSE
            SELECTCASE TALENT:TARGET:音楽知識
            CASE 1
                PRINTFORM 和一起合奏的
            CASE 2
                PRINTFORM 和一起伴唱的
            CASE 3
                PRINTFORM 和一起伴舞的
            ENDSELECT
        ENDIF
    CASE 9
        PRINTFORM 正在协助采集的
    CASE 10
        IF TFLAG:193 < 0
            PRINTFORM 正在观看钓鱼的
        ELSE
            PRINTFORM 和一起钓鱼的
        ENDIF
    CASE 12
        PRINTFORM 和一起闲逛的
    CASE 14
        PRINTFORM 正在帮忙购物的
    CASE 17
        PRINTFORM 正在帮忙堆雪人的
    ENDSELECT
CASE 403
    PRINTFORM 和一起休息的
CASE 410
    IF TFLAG:193
        PRINTFORM 和一起打扫的
    ELSE
        PRINTFORM 在看着的前面打扫的
    ENDIF
CASE 411
    PRINTFORM 和一起训练的 
CASE 440
    IF CHK_DATENOW(CFLAG:TARGET:约会中)
        PRINTFORM 和一起约会的
    ELSE
        PRINTFORM 刚好在附近的
    ENDIF

ENDSELECT
PRINTFORML %CALLNAME:MASTER%将、

IF EQUIP:TARGET:裙子
	PRINTFORM %SHOW_BOTTOM(TARGET,1)%
ELSEIF EQUIP:TARGET:和服
	PRINTFORM %SHOW_TOP(TARGET,1)%
ELSEIF EQUIP:TARGET:連衣裙
	PRINTFORM %SHOW_TOP(TARGET,2)%
ENDIF

PRINTFORM 被隐藏起来的

IF !EQUIP:TARGET:下半身内衣２
	PRINTFORM 裸露的下半身
ELSE
	PRINTFORM %PANTS_DESCRIPTION(EQUIP:TARGET:下半身内衣２, TARGET)%%PANTSNAME(EQUIP:TARGET:下半身内衣２, TARGET)%
ENDIF
PRINTL 看了个够…
WAIT

CALL KOJO_MESSAGE_SEND("PANCHIRA",0,TARGET)
CALL PANCHIRA_SOURCE

RETURN 1


@REV_PANCHIRA
DRAWLINE
IF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
    PRINTFORM 突然,狂暴的强风变得更加猛烈,%CALLNAME:MASTER%一时不察、
ELSE
    PRINTFORM 意外地,一阵恶作剧般的风从脚边卷起、%CALLNAME:MASTER%不防备、
ENDIF
IF EQUIP:MASTER:裙子 
    PRINTFORM %SHOW_BOTTOM(MASTER,1)%
ELSEIF EQUIP:MASTER:和服
    PRINTFORM %SHOW_TOP(MASTER,1)%的下摆 
ELSEIF EQUIP:MASTER:連衣裙
    PRINTFORM %SHOW_TOP(MASTER,2)%的下摆
ENDIF
PRINTFORM 被
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
    PRINTFORM 猛烈地

PRINTFORM 翻起来了

IF SELECTCOM == 368 && Activity_Type:TARGET == 10 && RAND:2 == 0
    PRINTFORML ,
    PRINTFORM 但专心钓鱼的%CALLNAME:TARGET%似乎没有注意到%CALLNAME:MASTER%暴露出的丑态...
    WAIT
    FLAG:風チラCT = 6
    RETURN 0
ELSE
    PRINTFORML ,
ENDIF 

IF SELECTCOM == 440
	IF CHK_DATENOW(CFLAG:TARGET:约会中)
		PRINTFORM 在附近
	ELSE
		PRINTFORM 偶然
	ENDIF
ENDIF

PRINTFORM %CALLNAME:MASTER%

SELECTCASE SELECTCOM

CASE 300
    PRINTFORM 和在附近聊天的
CASE 304
    IF CFLAG:TARGET:350 == 46
        PRINTFORM 邀请一起玩耍的
    ELSE
        PRINTFORM 在帮助工作的时候
    ENDIF
CASE 368
    SELECTCASE Activity_Type:TARGET
    CASE 0
        PRINTFORM 在考虑事情的时候
    CASE 1
        PRINTFORM 和一起玩耍的
    CASE 4
        PRINTFORM 在打扫的时候
    CASE 5
        PRINTFORM 和一起运动的
    CASE 6 
        PRINTFORM 在看着读书的
	CASE 8
        IF TFLAG:193 == -2
            SELECTCASE TALENT:TARGET:音楽知識
            CASE 1
                PRINTFORM 在目前演奏给看的
            CASE 2
                PRINTFORM 在目前唱歌给看的
            CASE 3
                PRINTFORM 在目前跳舞给看的
            ENDSELECT
        ELSE
            SELECTCASE TALENT:TARGET:音楽知識
            CASE 1
                PRINTFORM 配合合奏演奏给看的
            CASE 2
                PRINTFORM 配合伴奏唱歌给看的
            CASE 3
                PRINTFORM 配合伴奏跳舞给看的
            ENDSELECT
        ENDIF
    CASE 9
        PRINTFORM 在采集的时候得到帮助的 
    CASE 10
        IF TFLAG:193 < 0
            PRINTFORM 在目光所及的地方钓鱼的
        ELSE
            PRINTFORM 在旁边钓鱼的
        ENDIF
    CASE 12
        PRINTFORM 和一起悠闲的
    CASE 14
        PRINTFORM 在购物的时候得到帮助的
    CASE 17
        PRINTFORM 在堆雪人的时候得到帮助的
    ENDSELECT
CASE 403
    PRINTFORM 和一起休息的 
CASE 410
    IF TFLAG:193
        PRINTFORM 和一起打扫的
    ELSE
        PRINTFORM 看着打扫的样子
    ENDIF
CASE 411
    PRINTFORM 和一起训练的
CASE 440
    PRINTFORM 看着样子的
ENDSELECT
PRINTFORML 对%CALLNAME:TARGET% 
IF !EQUIP:MASTER:下半身内衣２
	PRINTFORM 裸露的下半身
ELSE
	PRINTFORM 穿在身上%PANTS_DESCRIPTION(EQUIP:MASTER:下半身内衣２, MASTER)%%PANTSNAME(EQUIP:MASTER:下半身内衣２, MASTER)%
ENDIF
PRINTL 完全被看到了…
WAIT

CALL KOJO_MESSAGE_SEND("REV_PANCHIRA",0,TARGET)
CALL REV_PANCHIRA_SOURCE

RETURN 1

@PANCHIRA_SOURCE
IF TALENT:恋人
	IF !EQUIP:TARGET:下半身内衣２
		SOURCE:TARGET:情愛 += 600
		SOURCE:TARGET:欲情 += 700
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:情愛 += 600
		SOURCE:TARGET:欲情 += 500
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:恋慕
	IF !EQUIP:TARGET:下半身内衣２
		SOURCE:TARGET:情愛 += 400
		SOURCE:TARGET:欲情 += 700
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:情愛 += 400
		SOURCE:TARGET:欲情 += 500
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:愛欲 || TALENT:炮友
	IF !EQUIP:TARGET:下半身内衣２
		SOURCE:TARGET:欲情 += 600
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:欲情 += 400
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:思慕
	IF !EQUIP:TARGET:下半身内衣２
		SOURCE:TARGET:欲情 += 300
		SOURCE:TARGET:誘惑 += 400
		SOURCE:TARGET:露出 += 800
		SOURCE:TARGET:逸脱 += 10
	ELSE
		SOURCE:TARGET:欲情 += 100
		SOURCE:TARGET:誘惑 += 100
		SOURCE:TARGET:露出 += 500
	ENDIF
ELSE
	IF !EQUIP:TARGET:下半身内衣２
		SOURCE:TARGET:露出 += 800
		SOURCE:TARGET:逸脱 += 20
		SOURCE:TARGET:反感 += 10
	ELSE
		SOURCE:TARGET:露出 += 500
		SOURCE:TARGET:反感 += 10
	ENDIF
ENDIF

CALL SOURCE_情愛(TARGET)
CALL SOURCE_欲情(TARGET)
CALL SOURCE_誘惑(TARGET)
CALL SOURCE_露出(TARGET)
CALL SOURCE_逸脱(TARGET)
CALL SOURCE_反感(TARGET)
FLAG:風チラCT = 6
CFLAG:TARGET:下半身内衣確認 = 1

RETURN 1



@REV_PANCHIRA_SOURCE
IF TALENT:MASTER:性別 == 2
	IF TALENT:恋人
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:侮辱 += 600
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 300
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:侮辱 += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:恋慕
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:侮辱 += 600
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 300
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:侮辱 += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:愛欲 || TALENT:炮友
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 300
			SOURCE:TARGET:侮辱 += 400
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:欲情 += 200
			SOURCE:TARGET:歓楽 += 300
			SOURCE:TARGET:侮辱 += 300
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:思慕
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:欲情 += 100
			SOURCE:TARGET:逸脱 += 20
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 100
			SOURCE:TARGET:侮辱 += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ELSE
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:逸脱 += 50
			SOURCE:TARGET:反感 += 50
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 50
			SOURCE:MASTER:露出 += 500
		ENDIF
	ENDIF
ELSE
	IF TALENT:恋人
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 600
			SOURCE:TARGET:侮辱 += 800
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:侮辱 += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:恋慕
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 600
			SOURCE:TARGET:侮辱 += 700
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:侮辱 += 400
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:愛欲 || TALENT:炮友
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:欲情 += 600
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:侮辱 += 700
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:侮辱 += 400
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:思慕
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 200
			SOURCE:TARGET:侮辱 += 300
			SOURCE:TARGET:逸脱 += 10
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 200
			SOURCE:TARGET:侮辱 += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ELSE
		IF !EQUIP:MASTER:下半身内衣２
			SOURCE:TARGET:歓楽 += 50
			SOURCE:TARGET:逸脱 += 10
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ENDIF
ENDIF

CALL SOURCE_情愛(TARGET)
CALL SOURCE_欲情(TARGET)
CALL SOURCE_歓楽(TARGET)
CALL SOURCE_侮辱(TARGET)
CALL SOURCE_逸脱(TARGET)
CALL SOURCE_反感(TARGET)
CALL SOURCE_露出(MASTER)
FLAG:風チラCT = 6

RETURN 1
