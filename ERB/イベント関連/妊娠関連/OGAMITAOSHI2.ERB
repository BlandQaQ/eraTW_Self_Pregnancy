@CHK_HININ_LOVE(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:恋人
	LOCAL += 500
;愛情経験

LOCAL += EXP:ARG:愛情経験 / 2

;好感度
SELECTCASE CFLAG:ARG:好感度
CASE 0 TO 499
	LOCAL -= 100
CASE 500 TO 999
	LOCAL += 50
CASE 1000 TO 2999
	LOCAL += 80
CASEELSE
	LOCAL += CFLAG:ARG:好感度 / 20
ENDSELECT

LOCAL += ABL:ARG:親密 * 25
LOCAL += ABL:ARG:従順 * 25
LOCAL += ABL:ARG:奉仕精神 * 10

RETURNF LOCAL

@CHK_HININ_REASON(ARG,ARGS)
#FUNCTION
#DIM 理性判定値
VARSET LOCAL

LOCAL:3 += GETPALAMLV(PALAM:欲情,25) * (10 + TALENT:ARG:快感応答 * 4)
LOCAL:3 += GETPALAMLV(PALAM:恭順,25) * (10 - TALENT:ARG:態度 * 3)
LOCAL:3 += GETPALAMLV(PALAM:好意,25) * 10

LOCAL:5 += ABL:ARG:膣射中毒 * 30
LOCAL:5 += ABL:ARG:欲望 * (10 + TALENT:ARG:快感応答 * 3)
LOCAL:5 += MARK:ARG:快楽刻印 * 30
LOCAL:5 += MARK:ARG:不埒刻印 * 10
LOCAL:5 += MARK:ARG:苦痛刻印 * 10
LOCAL:5 -= MARK:ARG:反発刻印 * 100


理性判定値 = 800 - LOCAL:3 * LOCAL:5 / 300

SIF ESTRUS_CYCLE(ARG) == 150
	理性判定値 += 1000

IF TCVAR:ARG:発情 && TALENT:ARG:動物耳 && CFLAG:ARG:900 == 7
	理性判定値 -= 500
ELSEIF TCVAR:ARG:催情薬 || IsHeat(ARG)
	理性判定値 -= 200
ENDIF
SIF IsInsenceValid(ARG)
	理性判定値 -= 50

IF TALENT:ARG:傲嬌
	IF TALENT:ARG:恋慕
		理性判定値 = 理性判定値 * 2 / 3
	ELSE
		理性判定値 = 理性判定値 * 3 / 2
	ENDIF
ENDIF

;信賴度
IF CFLAG:ARG:信賴度 < 100
	理性判定値 *= 4
ELSEIF CFLAG:ARG:信賴度 < 300
	理性判定値 *= 2
ELSEIF CFLAG:ARG:信賴度 < 500
	理性判定値 *= 5 / 3
ELSEIF CFLAG:ARG:信賴度 < 1000
	理性判定値 *= 6 / 5
ELSE
	理性判定値 *= 3 / 5
ENDIF

;カウンターだとここで素質処理しておく
IF ARGS == "COUNTER"
	SIF TALENT:ARG:抵抗
		理性判定値 += 200
	IF TALENT:ARG:自尊心 == -1
		理性判定値  = 理性判定値 * 9 / 10
	ELSEIF TALENT:ARG:自尊心 == 1
		理性判定値 = 理性判定値 * 6 / 5
	ENDIF
	SIF OTOKOGIRAI(ARG)
		理性判定値 = 理性判定値 * 3 / 2
	SIF TALENT:ARG:貞操 == 1
		理性判定値 = 理性判定値 * 6 / 5
	SIF TALENT:MASTER:謎之魅力
		理性判定値 = 理性判定値 * 2 / 5
	SIF TALENT:ARG:倒錯的
		理性判定値 = 理性判定値 * 4 / 5
	IF TALENT:ARG:難以越過的底線
		SELECTCASE EXP:ARG:愛情経験
			CASE IS >= 500
				理性判定値 = 理性判定値 / 3
			CASE IS >= 300
				理性判定値 = 理性判定値 * 11 / 10
			CASE IS >= 100
				理性判定値 = 理性判定値 * 5 / 4
			CASEELSE
				理性判定値 = 理性判定値 * 3 / 2
		ENDSELECT
	ENDIF
	IF TALENT:ARG:傲嬌
		IF TALENT:ARG:恋慕
			理性判定値 = 理性判定値 / 2
		ELSEIF TALENT:ARG:思慕
			理性判定値 = 理性判定値 * 2 / 3
		ELSE
			理性判定値 = 理性判定値 * 3 / 2
		ENDIF
	ENDIF
ENDIF


理性判定値 = MAX(理性判定値,0)
RETURNF 理性判定値

@OGAMITAOSHI2(ARG)
#DIM 理性判定値
#DIM 好意判定値
#DIM 好意表示
VARSET LOCAL


PRINTFORMW %CALLNAME:ARG%の胸中

好意判定値 = CHK_HININ_LOVE(ARG) 
CALL SHOW_LOVEPOWER(好意判定値)

WAIT
理性判定値 = CHK_HININ_REASON(ARG)

CALL SHOW_REASONWALL(理性判定値)
IF TALENT:MASTER:謎之魅力 
CALL COLORMESSAGE(@"%CALLNAME:ARG%感觉自己的理性受到了%CALLNAME:MASTER%謎之魅力的摇动......",C_PINK,1,1) 
	理性判定値 = 理性判定値 * 2 / 5
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:自尊心 == -1
	理性判定値  = 理性判定値 * 9 / 10
ELSEIF TALENT:ARG:自尊心 == 1
	理性判定値 = 理性判定値 * 6 / 5
    CALL COLORMESSAGE(@"骄傲的%CALLNAME:ARG%不会做出低价出卖自己的事......",C_PINK,1,1)
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:貞操 == 1
	理性判定値 = 理性判定値 * 6 / 5
    CALL COLORMESSAGE(@"有强烈贞操观念的%CALLNAME:ARG%犹豫了,不知道应不应该允许到这个地步......",C_PINK,1,1)
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF OTOKOGIRAI(ARG)
	理性判定値 = 理性判定値 * 3 / 2
	IF TALENT:ARG:性別嗜好 == 3
        CALL COLORMESSAGE(@"%CALLNAME:ARG%对他人普遍存在排斥情绪......",C_PINK,1,1)
    ELSEIF TALENT:ARG:性別嗜好 == 1
        CALL COLORMESSAGE(@"%CALLNAME:ARG%对男性普遍存在排斥情绪......",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:倒錯的
    CALL COLORMESSAGE(@"%CALLNAME:ARG%从堕落的关系中获得倒错的魅力......",C_PINK,1,1)
	理性判定値 = 理性判定値 * 4 / 5
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:難以越過的底線
	SELECTCASE EXP:ARG:愛情経験
		CASE IS >= 500
            CALL COLORMESSAGE(@"%CALLNAME:ARG%似乎相信只要是和%CALLNAME:MASTER%,一定会没问题......",C_PINK,1,1)
			理性判定値 = 理性判定値 / 3
		CASE IS >= 300
            CALL COLORMESSAGE(@"%CALLNAME:ARG%似乎相信只要是和%CALLNAME:MASTER%,一定会没问题......",C_PINK,1,1)
			理性判定値 = 理性判定値 * 11 / 10
		CASE IS >= 100
            CALL COLORMESSAGE(@"%CALLNAME:ARG%觉得即便是亲密的伙伴,也该保持一定距离......",C_PINK,1,1)
			理性判定値 = 理性判定値 * 5 / 4
		CASEELSE
            CALL COLORMESSAGE(@"%CALLNAME:ARG%不希望他人过于接近自己......",C_PINK,1,1)
			理性判定値 = 理性判定値 * 3 / 2
	ENDSELECT
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
IF TALENT:ARG:傲嬌
	IF TALENT:ARG:恋慕
		理性判定値 = 理性判定値 * 2 / 3
        CALL COLORMESSAGE(@"%CALLNAME:ARG%心动了!",C_PINK,1,1)
	ELSE
		理性判定値 = 理性判定値 * 3 / 2
        CALL COLORMESSAGE(@"%CALLNAME:ARG%对此感到抵触......",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定値)
ENDIF
WAIT
IF CHK_HININ_LOVE(ARG) > 理性判定値
		PRINTFORMW 
		TCVAR:ARG:外に出すから = 1
		;CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,6,0)
		PRINTW 同意在阴道外射精就可以不带套做!
ELSE
    PRINTFORML    %CALLNAME:ARG%婉言拒绝了
	;CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,2,0)
    CALL ASK_YN("尊重对方意愿","再努力一把")
	IF RESULT == 0
		TCVAR:ARG:挿入拒絶 = 1
		RETURN -1
	ELSE
	;口のうまさと相手次第では粘ればどうにかなる
		LOCAL = (4 - TALENT:ARG:応答 - TALENT:ARG:胆量 - TALENT:ARG:態度) * (ABL:MASTER:話術技能 * 5 + 3 * CFLAG:ARG:指出缺点)
		LOCAL:2 = 理性判定値 * (100 - LOCAL) / 100
		CALL SHOW_REASONWALL(LOCAL:2)
		IF CHK_HININ_LOVE(ARG) > LOCAL:2
            PRINTFORMW %CALLNAME:MASTER%凭借口才说服了%CALLNAME:ARG%
            TCVAR:ARG:外に出すから = 1
            ;CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,6,0)
            PRINTW 同意在阴道外射精就可以不带套做!
        ELSE
            CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,3,0)
            PRINTFORMW 被%CALLNAME:ARG%狠狠责骂了一顿...
			CFLAG:ARG:指出缺点 += 1
			CALL ENDUFUFU(ARG)
			CALL GET_ANGRY(ARG)
			MARK:ARG:反発刻印 = MIN(MARK:ARG:反発刻印 + 1, 3)
			TFLAG:106 = 0
			TCVAR:ARG:挿入拒絶 = 1
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SHOW_REASONWALL(理性判定値, UNIT = 100)
#DIM 理性判定値
#DIM UNIT

PRINTFORM 理性の壁%" " * STRLENS(CALLNAME:MASTER)%:
SETCOLOR 255,255,0
LOCAL = MAX(理性判定値 / UNIT,1)
PRINTFORM %"★" * LOCAL%
RESETCOLOR
SIF 理性判定値 % UNIT > (UNIT / 2)
	PRINTL ★
PRINTW

@SHOW_LOVEPOWER(好意判定値, UNIT = 100, ARGS)
#DIM 好意判定値
#DIM 好意表示
#DIM UNIT
IF ARGS == ""
	PRINTFORM %CALLNAME:MASTER%への好意:
ELSE
	PRINTFORM %ARGS%
ENDIF
好意表示 = MAX(好意判定値 / UNIT,0)
;LOCALS:1 = %GETFONT()%
;SETFONT "Times New Roman"
LOCAL = GETCOLOR()
PRINTFORM %UNICODE(0x2764) * MAX((好意表示 / 10000 -1),0)%
RESETCOLOR
SETCOLOR 255,51,153
SIF 好意表示 >=10000
	PRINTFORM %UNICODE(0x2764) * (9999)%%UNICODE(0x2764)%

PRINTFORM %UNICODE(0x2764) * (好意表示 % 10000)%
SETCOLOR LOCAL

SIF 好意判定値 % UNIT > (UNIT / 2)
	PRINTFORML %UNICODE(0x2764)%
SETFONT LOCALS:1
PRINTW
