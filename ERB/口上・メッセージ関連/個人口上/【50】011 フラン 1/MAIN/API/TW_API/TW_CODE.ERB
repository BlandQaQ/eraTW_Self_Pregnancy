;tw相关的临时代码
;COM文本
@k1011TwComMessageMain
	;地文
	CALL TRAIN_MESSAGE
	;主文本
	CALL k1011ComMessageMain
	;额外文本
	CALL k1011TwExMessage

;珠处理
@k1011TwPalamcngMain
	;调试函数
	TRYCALL K1011_TEST_PALAMCNG
	;数据处理
	CALL k1011PalacngBeforeDp
	;地文
	TRYCALL PALAM_MESSAGE_B
	;文本
	CALL k1011PalamcngMessageMain
	;数据处理
	CALL k1011PalamcngAfterDp

;额外的com处理
@k1011TwExMessage
	#DIM tmp
	SELECTCASE SELECTCOM
		;料理
		CASE 413
			;一起料理文本
			SIF DISH_ASSI == TARGET
				CALL k1011MakeBento
		;调合(药剂)
		CASE 446
			;选择的结果
			tmp = LOCAL@PHARMACY_CONSTITUTIONAL:1
			;使用药的人不少芙兰酱
			SIF tmp != TARGET
				RETURN
			SELECTCASE TFLAG:193
				;扶她药
				CASE 71
					CALL k1011FutaMedicine(0)
				;扶她消除药
				CASE 72
					CALL k1011FutaMedicine(1)
				;赤色糖果
				CASE 74
					CALL k1011RedCandy()
			ENDSELECT
	ENDSELECT

;角色信息
@k1011CharaInfo
	PRINTL ～悪魔之妹～　●種族：吸血鬼　●能力：破壞事物存在程度的能力
	PRINT  ・
	IF K1011Character == 1
		CALL k1011P("L", "天真烂漫模式", 0x60EEEE)
	ELSEIF K1011Character == 2
		CALL k1011P("L", "有点早熟模式", 0xFF80FF)
	ELSE
		THROW [芙兰口上ERROR]未知的性格差分K1011Character = {K1011Character}
	ENDIF
	PRINT  ・
	IF K1011InfOfSex == 1
		CALL k1011P("L", "对色情一无所知模式", 0x60EEEE)
	ELSEIF K1011InfOfSex == 2
		CALL k1011P("L", "对h感兴趣的模式", 0xFF80FF)
	ELSE
		CALL k1011P("L", "爱手淫的坏孩子模式", 0xFFCCFF)
	ENDIF
	PRINT  ・
	IF K1011NotMenchare
		IF k1011GetOtherInfo("未经初潮")
			CALL k1011P("L", "「初潮」尚未到来模式", 0x60EEEE)
			CALL k1011P("L", "　　　――在交往中初潮到来了、可以怀孕了")
		ELSE
			CALL k1011P("L", "「初潮」尚未到来模式", 0xFFCCFF)
			CALL k1011P("L", "　　　――在交往中初潮到来了、可以怀孕了")
		ENDIF
	ELSE
		CALL k1011P("L", "「初潮」已经到来的模式", 0xFFCCFF)
	ENDIF
	PRINT  ・
	IF K1011Future == 1
		CALL k1011P("L", @"%CALLNAME:MASTER%想让芙兰在某一天成为真正的女人", 0x60EEEE)
	ELSE
		CALL k1011P("L", @"%CALLNAME:MASTER%很喜欢有种坏掉的感觉的芙兰", 0xFF0F0F)
	ENDIF
	;8013 难度  0简单(无原版发疯事件) 1默认 2困难(原本困难) 3地狱(剧情杀) 4噩梦(7760)
	PRINT  ・
	SELECTCASE K1011Difficult
		CASE 0
			PRINTFORM 芙兰是个十分乖巧听话的孩子(
			;绿色
			SETCOLOR 0x44FF00
			PRINT 简单
			RESETCOLOR
			PRINTL )
		CASE 1
			PRINTFORM 芙兰是个偶尔会捣乱的孩子(
			;黄色
			SETCOLOR 0xFFFF00
			PRINT 普通
			RESETCOLOR
			PRINTL )
		CASE 2
			PRINTFORM 芙兰是个不听话的孩子(
			;橙色
			SETCOLOR 0xFFBC00
			PRINT 困难
			RESETCOLOR
			PRINTL )
		CASE 3
			PRINTFORM 芙兰喜欢把%CALLNAME:MASTER%撕成两半(
			;红色
			SETCOLOR 0xFF4400
			PRINT 地狱
			RESETCOLOR
			PRINTL )
		CASE 4
			PRINTFORM 你是来受折磨的模式(
			;紫色
			SETCOLOR 0xFF00CD
			PRINT 噩梦
			RESETCOLOR
			PRINTL )
	ENDSELECT
	PRINTL
	RETURN 1

;清晨事件
@k1011TwMorningEvent(flanWake)
	#DIM flanWake ;1已经醒了,0刚醒,-1还在睡
	#DIMS DYNAMIC type, 20
	#DIM DYNAMIC weight, 20
	#DIM DYNAMIC index
	#DIMS out
	;一言口上
	type:index = Normally
	weight:index = 30
	index ++
	;芙兰先醒后的清晨事件
	IF flanWake == 1
		;h
		IF CFLAG:既成事實 & 2
			;朝口交
			IF CFLAG:既成事實 & 1
				type:index = HM
				weight:index = k1011GetTalent("淫乱") * 3 + TALENT:炮友 * 3 + TALENT:愛欲 * 3 + k1011GetTalent("恋慕") + ABL:Ｍ感覚 * 2
				index ++
			ENDIF
			;朝乳交
			type:index = HB
			weight:index = k1011GetTalent("淫乱") * 3 + TALENT:炮友 * 2 + TALENT:愛欲 * 3 + k1011GetTalent("恋慕") + ABL:Ｂ感覚 * 2
			index ++
			;朝骑乘
			IF !k1011GetTalent("处女")
				type:index = HV
				weight:index = k1011GetTalent("淫乱") * 3 + TALENT:炮友 * 3 + TALENT:愛欲 * 3 + k1011GetTalent("恋慕") + ABL:Ｖ感覚 * 2
				index ++
			ENDIF
			;朝骑乘+揉胸
			IF !k1011GetTalent("处女")
				type:index = HVB
				weight:index = k1011GetTalent("淫乱") * 3 + TALENT:炮友 * 3 + TALENT:愛欲 * 3 + k1011GetTalent("恋慕") + ABL:Ｖ感覚 + ABL:Ｂ感覚
				index ++
			ENDIF
			;朝骑乘位肛交
			IF CFLAG:既成事實 & 8
				type:index = HA
				weight:index = k1011GetTalent("淫乱") * 3 + TALENT:炮友 * 3 + TALENT:愛欲 * 3 + k1011GetTalent("恋慕") + ABL:Ａ感覚 * 2
				index ++
			ENDIF
		ENDIF
		;纯爱系
		IF k1011GetTalent("恋慕")
			type:index = LOVE
			weight:index = k1011GetTalent("恋慕") * 3 + k1011GetTalent("亲爱") * 3 + k1011GetTalent("恋人") * 3 + k1011GetTalent("相爱") * 3 + ABL:料理技能 * 2
			index ++

			type:index = Kiss
			weight:index = k1011GetTalent("恋慕") * 3 + k1011GetTalent("亲爱") * 3 + k1011GetTalent("恋人") * 3 + k1011GetTalent("相爱") * 3 + ABL:Ｍ感覚 * 2
			index ++
		ENDIF
	ENDIF
	;随机事件
	CALLFORM k1011TwMorningEvent%type:(k1011WRand(weight))%
	;相爱取得
	CALL k1011TwLove3DayStart

;清晨一言口上
@k1011TwMorningEventNormally
	CALL k1011GoodMorning

;朝口交
@k1011TwMorningEventHM
	CALL k1011MorningHM
	CALL k1011AddExp("侍奉快乐经验", 2)
	CALL k1011AddExp("口交经验", 2)
	CALL k1011AddExp("精液经验", 2)
	CALL k1011AddExp("饮精绝顶经验", 2)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)

;朝乳交
@k1011TwMorningEventHB
	CALL k1011MorningHB
	CALL k1011AddExp("侍奉快乐经验", 2)
	CALL k1011AddExp("绝顶经验", 2)
	CALL k1011AddExp("精液经验", 2)
	SIF k1011GetTalent("母乳体质")
		CALL k1011AddExp("喷乳经验", 2)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)
	CALL k1011AddPalam("快Ｂ", 9999)

;朝骑乘
@k1011TwMorningEventHV
	CALL k1011MorningHV
	CALL k1011AddExp("侍奉快乐经验", 2)
	CALL k1011AddExp("绝顶经验", 2)
	CALL k1011AddExp("精液经验", 2)
	CALL k1011AddExp("Ｖ経験", 2)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)
	CALL k1011AddPalam("快Ｖ", 9999)

;朝骑乘+揉胸
@k1011TwMorningEventHVB
	CALL k1011MorningHVB
	CALL k1011AddExp("侍奉快乐经验", 2)
	CALL k1011AddExp("绝顶经验", 2)
	CALL k1011AddExp("精液经验", 2)
	CALL k1011AddExp("Ｖ経験", 1)
	SIF k1011GetTalent("母乳体质")
		CALL k1011AddExp("喷乳经验", 1)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)
	CALL k1011AddPalam("快Ｖ", 9999)
	CALL k1011AddPalam("快Ｂ", 9999)

;朝骑乘位肛交
@k1011TwMorningEventHVA
	CALL k1011MorningHA
	CALL k1011AddExp("侍奉快乐经验", 2)
	CALL k1011AddExp("绝顶经验", 2)
	CALL k1011AddExp("精液经验", 2)
	CALL k1011AddExp("Ａ経験", 2)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)
	CALL k1011AddPalam("快Ａ", 9999)

;朝新妻
@k1011TwMorningEventLove
	CALL k1011MorningLove
	CALL k1011AddExp("烹饪经验", 2)
	CALL k1011AddJuel("习得", 1000)

;朝Kiss
@k1011TwMorningEventKiss
	CALL k1011MorningKiss
	CALL k1011AddExp("绝顶经验", 2)
	CALL k1011AddJuel("习得", 300)
	CALL k1011AddPalam("欲情", 1000)

;回合结束主函数
@k1011TwTurnEndMain
	;纯爱查看
	CALL k1011TwLoveCheck()
	;调教后继续h
	SIF CFLAG:現在位置 == CFLAG:MASTER:現在位置
		CALL k1011TwDayendH()
	;夜爬
	SIF GET_MAPID(CFLAG:現在位置) == GET_MAPID(CFLAG:MASTER:現在位置)
		CALL k1011TwNightEvent()
	;DP
	CALL k1011TurnEndMain
	;相爱
	CALL k1011TwLove3DayEnd()

;调教后继续h
@k1011TwDayendH()
	#DIM hTimes
	;你先生要有男性的象征
	SIF !(k1011GetTalent("性别", MASTER) & 2)
		RETURN 0
	;必须陷落
	SIF !(k1011GetTalent("恋慕") || TALENT:愛欲)
		RETURN 0
	;不能是处女捏
	SIF k1011GetTalent("处女")
		RETURN 0
	;体力必要
	SIF k1011GetBase("体力") < 500 || k1011GetBase("体力", MASTER) < 500
		RETURN 0

	;Ｖ感觉
	IF k1011GetAbl("Ｖ感觉") <= 3
		hTimes = 0
	ELSEIF k1011GetAbl("Ｖ感觉") == 3
		hTimes = 1
	ELSEIF k1011GetAbl("Ｖ感觉") == 4
		hTimes = 2
	ELSE
		hTimes = 4
	ENDIF

	;次数太少
	SIF hTimes < 1
		RETURN 0

	IF CFLAG:积攒度 >= 800
		TIMES hTimes, 2.00
	ELSEIF CFLAG:积攒度 >= 600
		TIMES hTimes, 1.30
	ELSEIF CFLAG:积攒度 >= 400
		TIMES hTimes, 1.00
	ELSEIF CFLAG:积攒度 >= 200
		TIMES hTimes, 0.75
	ELSE
		TIMES hTimes, 0.5
	ENDIF

	DRAWLINE
	;地文
	PRINTFORML 一天结束了，%CALLNAME:MASTER%正要上床的时，欲求不满的%CALLNAME:TARGET%突然\@ CFLAG:現在位置 == CFLAG:MASTER:現在位置 ? 推到了%CALLNAME:MASTER% # 来到了屋子里\@
	PRINTFORML 他就这样直接推倒了%CALLNAME:MASTER%，把阴茎送入了阴道内，激烈地摆动起腰来
	PRINTFORML %CALLNAME:TARGET%进行享受着%CALLNAME:MASTER%的小穴…
	PRINTFORMW 和%CALLNAME:TARGET%做了{hTimes}次后，心满意足的两人互相怀抱着，就这样交合着陷入了沈眠
	;口上
	IF RAND:2 == 0
		CALL k1011TrainEndV
	ELSE
		CALL k1011TrainEndNight
	ENDIF
	CALL k1011AddExp("Ｖ经验")
	CALL k1011AddExp("精液经验")
	CFLAG:积攒度 -= hTimes * 50
	CFLAG:积攒度 = MAX(0, CFLAG:积攒度)

;夜爬
@k1011TwNightEvent()
	;没开启日常事件扩展
	SIF !K1011Config:exDaily
		RETURN
	SIF RAND:(CFLAG:好感度 + CFLAG:信賴度 * 5) > 3000
		CALL k1011NightEventMain()

;变性
@k1011TwChangeSex, chara
	#DIM chara
	SELECTCASE TALENT:chara:性別
		;女
		CASE 1
			TALENT:chara:性別 = 2
			MAXBASE:chara:2 = 10000
			MAXBASE:chara:精力 = 1000
			MAXBASE:chara:勃起 = 1500
			SIF BASE:chara:精力 > 1000
				BASE:chara:精力 = 1000
		;男
		CASE 2
			TALENT:chara:性別 = 1
			MAXBASE:chara:2 = 0
			MAXBASE:chara:精力 = 0
			TALENT:chara:120 = 0
		;女(扶她)
		CASE 3
			TALENT:chara:性別 = 2
		;ERROR
		CASE 4
			THROW [芙兰口上ERROR] 未知性别
	ENDSELECT

;每天见面时的思慕/恋慕查看
@k1011LoveCheck
	IF k1011GetTalent("亲爱")
		IF !K1011TrainStartFirst:亲爱取得
			CALL k1011MeetGetLove2()
			K1011TrainStartFirst:亲爱取得 = 1
			RETURN 1
		ENDIF
	ELSEIF k1011GetTalent("恋慕")
		IF !K1011TrainStartFirst:恋慕取得
			CALL k1011MeetGetLove1()
			K1011TrainStartFirst:恋慕取得 = 1
			RETURN 1
		ENDIF
	ENDIF
	IF k1011GetTalent("相爱")
		IF !K1011TrainStartFirst:相爱取得
			CALL k1011MeetGetLove3()
			K1011TrainStartFirst:相爱取得 = 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0

;只有芙兰有陷落
@k1011OnlyFallenCheck(chara = 50)
	#FUNCTION
	#DIM chara
	SIF !GROUPMATCH(FINDCHARA(TALENT:思慕, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDLASTCHARA(TALENT:思慕, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDCHARA(TALENT:恋慕, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDLASTCHARA(TALENT:恋慕, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDCHARA(TALENT:愛欲, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDLASTCHARA(TALENT:愛欲, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDCHARA(TALENT:炮友, 1), -1, chara)
		RETURNF 0
	SIF !GROUPMATCH(FINDLASTCHARA(TALENT:炮友, 1), -1, chara)
		RETURNF 0
	RETURNF 1

;tw的相爱
@k1011TwLove3GetCondition
	;未启用
	SIF K1011TwLove3 != 1
		RETURN 0
	SIF MONEY - K1011TwBaseMoney < 1000000
		RETURN 0
	SIF ABL:従順 + ABL:親密 + ABL:欲望 + ABL:奉仕精神 < 100
		RETURN 0
	SIF !TALENT:恋慕
		RETURN 0
	RETURN 1

;tw的相爱
@k1011TwLove3GetAction
	;其他人已经陷落了
	IF !k1011OnlyFallenCheck()
		PRINTFORMW 你已经有其他妹子了、不用再努力买戒指了
		K1011TwLove3 = -1
		RETURN
	ENDIF
	CALL k1011AskYn("要花费100w为芙兰购买订婚戒指吗?")
	IF RESULT
		MONEY -= 1000000
		CALL k1011GetEnding, "k1011SingleEnding"
		CALL k1011SetTalent("相爱", 1)
		K1011TwLove3 = 2
	ENDIF

;tw的相爱
@k1011TwLove3DayEnd
	;相爱禁用
	SIF K1011TwLove3 == -1
		RETURN
	;相爱已取得
	SIF k1011GetTalent("相爱")
		RETURN
	;不喜欢芙兰禁用
	IF !k1011Love()
		K1011TwLove3 = -1
		RETURN
	ENDIF
	;过期限
	;不知道是否启用
	IF K1011TwLove3 == 0
		;和其他人有陷落了
		IF !k1011OnlyFallenCheck()
			K1011TwLove3 = -1
			RETURN
		ENDIF
		CALL k1011AskYn("想和可爱的芙兰酱结婚吗?")
		;同意
		IF RESULT
			PRINTFORMW 但这不是ym可怎么办呢?
			PRINTFORMW 问题不大、我直接把结婚系统搬过来
			CALL k1011TwLove3ChoiceDifficult()
			K1011TwLove3 = 1
			DRAWLINE
			PRINTL ~订婚戒指出现条件~
			PRINTL 芙兰的亲密+顺从+欲望+侍奉精神合计为100以上
			PRINTL 没有反发刻印与反发履历
			PRINTL 芙兰必须取得[恋慕]
			PRINTL 其他人不能有陷落([思慕]/[恋慕]/[炮友]/[爱欲])
			PRINTL 要在期限内凑够目标金额买戒指
			DRAWLINE
			PRINTFORMW 为了和ym玩家有相同的待遇、就把你现在的钱视为开局的25000了
			IF MONEY < 25000
				PRINTFORMW 哈、你连25000都没……得了、送你点钱吧
				MONEY = 25000
			ENDIF
			K1011TwBaseMoney = MONEY - 25000
		;拒绝
		ELSE
			K1011TwLove3 = -1
		ENDIF
	;启用
	ELSE
		;和其他人有陷落了
		IF !k1011OnlyFallenCheck()
			K1011TwLove3 = -1
			SETCOLOR 0x747474
			PRINTFORML 你已经有其他女孩了、没必要继续为订婚戒指努力了
			RESETCOLOR
			RETURN
		ENDIF
		IF MONEY - K1011TwBaseMoney >= 1000000
			IF ABL:従順 + ABL:親密 + ABL:欲望 + ABL:奉仕精神 < 100
				SETCOLOR color_mix(0xff5050, MAX(100 - (ABL:従順 + ABL:親密 + ABL:欲望 + ABL:奉仕精神), 0) , 0x50ff50, ABL:従順 + ABL:親密 + ABL:欲望 + ABL:奉仕精神)
				PRINT 亲密+顺从+欲望+侍奉精神合计距100还剩
				FONTBOLD
				PRINTVL 100 - (ABL:従順 + ABL:親密 + ABL:欲望 + ABL:奉仕精神)
				FONTREGULAR
				RESETCOLOR
				RETURN
			ENDIF
			IF !TALENT:恋慕
				SETCOLOR 0xff5050
				PRINTL 恋慕未取得
				RESETCOLOR
				RETURN
			ENDIF
			SETCOLOR 0x50ff50
			PRINTL 已经凑够钱了、可以在芙兰的【口上菜单】里购买戒指了
		ELSE
			SETCOLOR color_mix(0xff5050, MAX(1000000 - (MONEY - K1011TwBaseMoney), 0) , 0x50ff50, MAX(MONEY - K1011TwBaseMoney, 0))
			PRINT 距离100w还剩
			FONTBOLD
			PRINTS k1011YmMoneyShow(MONEY)
			FONTREGULAR
			PRINTL 圆
			RESETCOLOR
		ENDIF
	ENDIF

;tw的相爱
@k1011TwLove3DayStart
	;相爱禁用
	SIF K1011TwLove3 == -1
		RETURN
	;相爱已取得
	SIF k1011GetTalent("相爱")
		RETURN
	K1011TwDealine --
	;期限未到
	IF K1011TwDealine
		PRINT 还剩
		SETCOLOR 0xff5050
		PRINTV K1011TwDealine
		RESETCOLOR
		PRINTL 天
	;期限结束
	ELSE
		SETCOLOR 0xff5050
		FONTBOLD
		PRINTW 很遗憾……期限已到
		PRINTW 这一周目是没法和芙兰结婚了
		FONTSTYLE
		RESETCOLOR
		K1011TwLove3 = -1
	ENDIF

;以ym的格式展示日期
@k1011YmMoneyShow(num)
	#FUNCTIONS
	#DIM num
	#DIMS out, 3
	SIF num >= 10000
		out:1 = {num / 10000}萬
	SIF LOCAL % 10000 > 0
		LOCALS:2 = {LOCAL % 10000}
	out '= out:1 + out:2
	RETURNF out



@k1011TwLove3ChoiceDifficult
	DRAWLINE
	PRINTL ★★请选择难度★★
	PRINTL 
	DRAWLINE
	SETCOLOR COLOR("P_GREEN")
	PRINTFORML [1]EASY     (期限为{k1011GetDealine(1), 3}日　目标金额:100W)
	SETCOLOR COLOR("AQUA")
	PRINTFORML [2]NORMAL   (期限为{k1011GetDealine(2), 3}日　目标金额:100W)
	SETCOLOR COLOR("P_BLUE")
	PRINTFORML [3]HARD     (期限为{k1011GetDealine(3), 3}日　目标金额:100W)
	SETCOLOR COLOR("P-RED")
	PRINTFORML [4]LUNATIC  (期限为{k1011GetDealine(4), 3}日　目标金额:100W)
	SETCOLOR COLOR("RED")
	PRINTFORML [5]PHANTASM (期限为{k1011GetDealine(5), 3}日　目标金额:100W)
	PRINTFORML [6]ULTRA    (期限为{k1011GetDealine(6), 3}日　目标金额:100W)
	PRINTFORML [7]OVERDRIVE(期限为{k1011GetDealine(7), 3}日　目标金额:100W)
	RESETCOLOR
	SETCOLOR COLOR("ORANGE")
	PRINTL [10]HELP    (各个难度简单的说明）
	RESETCOLOR
	$INPUT_LOOP_DIFFICULTY
	INPUT
	IF RESULT == 10
		DRAWLINE
		SETCOLOR COLOR("P_GREEN")
		PRINTL EASY     ：面向想要轻松游玩者    部分机制受限
		SETCOLOR COLOR("AQUA")
		PRINTL NORMAL   ：面向想要正常游玩者    部分机制受限
		SETCOLOR COLOR("P_BLUE")
		PRINTL HARD     ：面向接受较难流程者
		SETCOLOR COLOR("P-RED")
		PRINTL LUNATIC  ：面向拥有受虐倾向者    有特别机制
		SETCOLOR COLOR("RED")
		PRINTL PHANTASM ：面向极度渴望受虐者    有特别机制
		PRINTL ULTRA    ：面向愿为刻符折磨者    有特别机制
		PRINTL OVERDRIVE：面向乐见荒谬世界者    有特别机制
		SETCOLOR 0x747474
		PRINTL (因为这不是ym,所以啥机制都没)
		RESETCOLOR
		DRAWLINE
		WAIT
		DRAWLINE
		DRAWLINE
		DRAWLINE
		RESTART
	ELSEIF RESULT == 1
		PRINT 难度：“
		SETCOLOR COLOR("P_GREEN")
		PRINT EASY
		RESETCOLOR
		PRINTW ”已选择
	ELSEIF RESULT == 2
		PRINT 难度：“
		SETCOLOR COLOR("aqua")
		PRINT NORMAL
		RESETCOLOR
		PRINTW ”已选择
	ELSEIF RESULT == 3
		PRINT 难度：“
		SETCOLOR COLOR("P_BLUE")
		PRINT HARD
		RESETCOLOR
		PRINTW ”已选择
	ELSEIF RESULT == 4
		PRINT 难度：“
		SETCOLOR COLOR("P-RED")
		PRINT LUNATIC
		RESETCOLOR
		PRINTW ”已选择
		; ;初期ステータス：受虐狂
		; TALENT:MASTER:受虐狂 = 1
	ELSEIF RESULT == 5
		PRINT 难度：“
		SETCOLOR COLOR("RED")
		PRINT PHANTASM
		RESETCOLOR
		PRINTW ”已选择
		; ;主人の技巧を強制的に1にする
		; ABL:MASTER:技巧 = 1
		; ;初期ステータス：抖M
		; TALENT:MASTER:抖M = 1
	ELSEIF RESULT == 6
		PRINT 难度：“
		SETCOLOR COLOR("RED")
		PRINT ULTRA
		RESETCOLOR
		PRINTW ”已选择
		; ;主人の技巧を強制的に1にする
		; ABL:MASTER:技巧 = 1
		; ;初期ステータス：疯狂
		; TALENT:MASTER:疯狂 = 1
		DRAWLINE
		SETCOLOR COLOR("P-RED")
		ALIGNMENT CENTER
		PRINTL 
		PRINTL “Welcome to Special Round”
		PRINTL 
		RESETCOLOR
		DRAWLINE
		WAIT
		ALIGNMENT LEFT
	ELSEIF RESULT == 7
		PRINT 难度：“
		SETCOLOR COLOR("RED")
		PRINT OVERDRIVE
		RESETCOLOR
		PRINTW ”已选择
		; ;主人の技巧を強制的に1にする
		; ABL:MASTER:技巧 = 1
		; ;初期ステータス：疯狂
		; TALENT:MASTER:疯狂 = 1
		DRAWLINE
		SETCOLOR COLOR("RED")
		ALIGNMENT CENTER
		PRINTL 
		PRINTL “欢迎来到地狱”
		PRINTL 
		RESETCOLOR
		DRAWLINE
		WAIT
		ALIGNMENT LEFT
	ELSE
		GOTO INPUT_LOOP_DIFFICULTY
	ENDIF
	;期限设定
	K1011TwDealine = k1011GetDealine(RESULT) + 1

;根据难度获得期限
@k1011GetDealine(difficult)
	#FUNCTION
	#DIM difficult
	SELECTCASE difficult
		;EASY(1周目のみ期限＋0日)
		CASE 1
			RETURNF 120
		;NORMAL
		CASE 2
			RETURNF 100
		;HARD
		CASE 3
			RETURNF 90
		;LUNATIC(
		CASE 4
			RETURNF 80
		;PHANTASM
		CASE 5
			RETURNF 70
		;ULTRA
		CASE 6
			RETURNF 50
		;OVERDRIVE
		CASE 7
			RETURNF 40
		;UNKOWN
		CASEELSE
			RETURNF 100
	ENDSELECT

;醉酒情况,从76哪里cp的
;0没醉酒,剩下看注释
@k1011Drunk(chara = -1)
	#FUNCTION
	#DIM chara
	SIF chara == -1
		chara = TARGET
	SIF BASE:chara:酒気 <= 2
		RETURNF 0
	SELECTCASE BASE:chara:酒気
		;不省人事
		CASE IS > (MAXBASE:chara:酒気 / 10) * 8
			RETURNF 3
		;酔酒
		CASE IS > (MAXBASE:chara:酒気 / 10) * 5
			RETURNF 2
		;微醺
		CASE IS <= (MAXBASE:chara:酒気 / 10) * 5
			RETURNF 1
	ENDSELECT

;tw初见口上
@k1011TwMeet()
	CALL k1011GPC("L", @"「人类?是没见过的人类捏」", TARGET)
	CALL k1011GP("L", @"你先生打量着面前的萝莉少女、鲜红的眼睛、尖尖的虎牙、看样子应该是吸血鬼吧?")
	CALL k1011GP("L", @"但奇怪的是背后却有一个奇奇怪怪的翅膀……")
	CALL k1011GP("L", @"嗯……反正妖怪总是奇奇怪怪的、这样解释就合理了")
	CALL k1011GP("L", @"光愣在这里欣赏翅膀也不怎么好、于是你先生挥了挥手向少女做了回应")
	CALL k1011GP("L", @"看到你先生举起了手、少女的好奇大眼睛瞬间睁大了、直接叫出生来")
	CALL k1011GPC("L", @"「哇、手可以动唉……真的是人类唉」", TARGET)
	CALL k1011GP("L", @"紧接着兴奋的扑腾着小翅膀、跑到了你先生面前")
	CALL k1011GPC("L", @"「好神奇、比小熊先生有趣多了」", TARGET)
	CALL k1011GPC("L", @"「我是芙兰朵露·斯卡雷特、该怎么称呼你啊?能和我一起玩吗?」", TARGET)
	CALL k1011CallMasterNameInput()
	CALL k1011GPC("L", @"「哥哥大人、哥哥大人、快和芙兰一起玩弹幕游戏」", TARGET)
	
;tw的指令前dp
@k1011TwBeforeCom
	;单恋人
	SIF k1011Love() && SELECTCOM == 352 && TALENT:MASTER:7
		TFLAG:192 = -1

@k1011TwButtonCondition(id)
	#FUNCTION
	#DIM id
	#DIM chara
	chara = FINDCHARA(NO, 50)
	IF custom_button_name:chara:id == ""
		RETURNF 0
	ELSE
		RETURNF 1
	ENDIF
