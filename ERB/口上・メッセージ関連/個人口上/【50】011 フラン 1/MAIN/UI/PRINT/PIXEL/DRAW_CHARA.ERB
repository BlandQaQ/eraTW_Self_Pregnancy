;========================================================
;绘制人物立绘
;========================================================
@k1011GalPDraw
	#DIM baseColor, 27, 999
	#DIM width
	#DIMS DYNAMIC textList, 5
	#DIM DYNAMIC colorList, 5, 999
	#DIMS DYNAMIC wordList, 27, 999
	VARSET baseColor, GETBGCOLOR()
	width = k1011GetWidth() / 2
	;上面清空
	PRINTV "\n" * (k1011GetHeight() - 27)
	;芙兰
	SIF k1011GCharaNo() == 1011
		CALL k1011GalPDarwFlan, baseColor

	;生成文字
	CALL k1011GalPGetTextList, textList, colorList

	;框
	CALL k1011GalPBox, baseColor, wordList, textList, colorList

	CALL k1011GalPDrawText, wordList, baseColor, width

;画芙兰
@k1011GalPDarwFlan, baseColor
	#DIM REF baseColor, 0, 0
	#DIMS strList, 27
	;芙兰状态
	CALL k1011GalPImgInfo
	;翅膀
	CALLFORM k1011GalImgWing{K1011GalWing}, strList
	CALL k1011GalPAddImg, baseColor, strList
	;身体
	CALL k1011GalImgBody, strList
	CALL k1011GalPAddImg, baseColor, strList
	;胳膊
	IF K1011GalMood == "Happy"
		CALL k1011GalImgSeptumHappy, strList
	ELSE
		CALL k1011GalImgSeptum, strList
	ENDIF
	CALL k1011GalPAddImg, baseColor, strList
	;普通衣服
	CALL k1011GalImgClothesBodyNormally, strList
	CALL k1011GalPAddImg, baseColor, strList
	;普通袖子
	IF K1011GalMood == "Happy"
		CALL k1011GalImgClothesSleeveHappyNormally, strList
	ELSE
		CALL k1011GalImgClothesSleeveNormally, strList
	ENDIF
	CALL k1011GalPAddImg, baseColor, strList
	;嘴
	SELECTCASE K1011GalMouth
		CASE 0
			CALL k1011GalImgFaceMouthOff, strList
		CASE 1
			CALL k1011GalImgFaceMouthOn0, strList
		CASE 2
			CALL k1011GalImgFaceMouthOn1, strList
		CASEELSE
			THROW [芙兰口上ERROR]错误的嘴型(K1011GalMouth = {K1011GalMouth})
	ENDSELECT
	CALL k1011GalPAddImg, baseColor, strList
	;眼
	SELECTCASE K1011GalEye
		CASE 0
			CALL k1011GalImgFaceEye, strList
		CASE 1
			CALL k1011GalImgFaceEyeHalf0, strList
		CASE 2
			CALL k1011GalImgFaceEyeHalf1, strList
		CASE 3
			CALL k1011GalImgFaceEyeOff, strList
		CASEELSE
			THROW [芙兰口上ERROR]错误的眼型(K1011GalEye = {K1011GalEye})
	ENDSELECT
	CALL k1011GalPAddImg, baseColor, strList
	;zun帽
	CALL k1011GalImgZun, strList
	CALL k1011GalPAddImg, baseColor, strList
	;心情
	SELECTCASE K1011GalMood
		CASE "Love"
			CALL k1011GalImgLove, strList
			CALL k1011GalPAddImg, baseColor, strList
		CASE "Angry"
			CALL k1011GalImgAngry, strList
			CALL k1011GalPAddImg, baseColor, strList
		CASE "Sing"
			CALL k1011GalImgSing, strList
			CALL k1011GalPAddImg, baseColor, strList
		CASE "Confuse"
			CALL k1011GalImgConfuse, strList
			CALL k1011GalPAddImg, baseColor, strList
		CASE "Suprise"
			CALL k1011GalImgSuprise, strList
			CALL k1011GalPAddImg, baseColor, strList
	ENDSELECT

;绘制对话框
@k1011GalPBox, baseColor, wordList, textList, colorList
	#DIM REF baseColor, 0, 0
	#DIMS REF textList, 0
	#DIMS REF wordList, 0, 0
	#DIM REF colorList, 0, 0
	#DIM whiteColor
	#DIM width
	#DIM posY
	#DIM posX
	#DIM long
	width = k1011GetWidth()
	whiteColor = rgb_to_hex(192,192,192)
	FOR posY, 20, 27
		baseColor:posY:0 = GETBGCOLOR()
		baseColor:posY:1 = GETBGCOLOR()
		baseColor:posY:2 = whiteColor
		long = 6
		;上下边框
		IF posY == 20 || posY == 26
			FOR posX, 3, 9999
				baseColor:posY:posX = whiteColor
				long += 2
				SIF long >= width - 4
					BREAK
			NEXT
		;文字
		ELSE
			FOR posX, 3, 9999
				;有文字
				IF posX - 3 < STRLENSU(textList:(posY - 21))
					baseColor:posY:posX = colorList:(posY - 21):(posX - 3)
					wordList:posY:posX '= SUBSTRINGU(textList:(posY - 21), posX - 3, 1)
					long += STRLENS(wordList:posY:posX)
				;无文字
				ELSE
					;奇数
					IF long % 2
						wordList:posY:posX '= " "
						baseColor:posY:posX = GETBGCOLOR()
						long += 1
					;正常
					ELSE
						IF long >= width - 6
							baseColor:posY:posX = whiteColor
							long += 2
						ELSE
							baseColor:posY:posX = GETBGCOLOR()
							long += 2
						ENDIF
					ENDIF
				ENDIF
				SIF long >= width - 4
					BREAK
			NEXT
		ENDIF
	NEXT

;增加图层
@k1011GalPAddImg, baseColor, strList
	#DIM REF baseColor, 0, 0
	#DIMS REF strList, 0
	#DIM posX
	#DIM posY
	#DIMS DYNAMIC colorList, 999
	#DIM colorLong
	#DIMS arbg, 4
	#DIM alpha
	#DIM rbg, 3
	;绘制每一行
	FOR posY, 0, MIN(VARSIZE("baseColor", 0), VARSIZE("strList"))
		SPLIT strList:posY, "|", colorList
		colorLong = RESULT
		;绘制每一像素
		FOR posX, 0, MIN(VARSIZE("baseColor", 1), colorLong)
			SIF colorList:posX == "0"
				CONTINUE
			SPLIT colorList:posX, "_", arbg
			alpha = TOINT(arbg:0)
			rbg:0 = TOINT(arbg:1)
			rbg:1 = TOINT(arbg:2)
			rbg:2 = TOINT(arbg:3)
			baseColor:posY:posX = color_mix(rgb_to_hex(rbg:0, rbg:1, rbg:2), alpha, baseColor:posY:posX, 255 - alpha)
		NEXT
	NEXT

;生成文字
@k1011GalPGetTextList, textList, colorList
	#DIMS REF textList, 0
	#DIM REF colorList, 0, 0
	#DIM index
	#DIM long
	#DIMS DYNAMIC colorStr, 999
	#DIM DYNAMIC posY
	#DIM DYNAMIC posX
	#DIMS char
	#DIM width
	;计算宽度
	width = k1011GetWidth() - 12
	;得到颜色列表
	SPLIT K1011GalTextColor, "_", colorStr
	;录入
	FOR index, 0, STRLENSU(K1011GalText)
		char '= SUBSTRINGU(K1011GalText, index, 1)
		;换行除外
		IF char != "\n"
			textList:posY += char
			colorList:posY:posX = TOINT(colorStr:index)
		ENDIF
		posX ++
		;换行
		IF STRLENS(textList:posY) >= width || char == "\n"
			posX = 0
			posY ++
			;超出最大行数
			SIF posY >= 5
				BREAK
		ENDIF
	NEXT

;打印
@k1011GalPDrawText, wordList, baseColor, width
	#DIM REF baseColor, 0, 0
	#DIMS REF wordList, 0, 0
	#DIM width
	#DIM long
	#DIM posX
	#DIM posY
	FOR posY, 0, VARSIZE("baseColor", 0)
		long = 0
		FOR posX, 0, VARSIZE("baseColor", 1)
			SETCOLOR baseColor:posY:posX
			IF wordList:posY:posX == ""
				PRINTPLAIN ■
				long += 2
			ELSE
				PRINTS wordList:posY:posX
				long += STRLENS(wordList:posY:posX)
			ENDIF
			SIF long >= width * 2
				BREAK
		NEXT
		PRINTL
	NEXT
	;重置颜色
	RESETCOLOR

;更新图片
@k1011GalPImgInfo
	IF K1011CarttonFlow == -1
		K1011GalMouth = 0
		K1011GalEye = 0
	ENDIF
	;嘴巴
	IF K1011CarttonFlow % 4 == 0
		IF K1011GalMouth != 1
			K1011GalMouth = 1
		ELSE
			K1011GalMouth = 0
		ENDIF
	ENDIF
	;眼睛
	IF K1011CarttonFlow % 30 == 0
		K1011GalEye = 1
	ELSEIF K1011CarttonFlow % 30 == 1
		K1011GalEye = 2
	ELSEIF K1011CarttonFlow % 30 == 2
		K1011GalEye = 3
	ELSEIF K1011CarttonFlow % 30 == 3
		K1011GalEye = 3
	ELSEIF K1011CarttonFlow % 30 == 4
		K1011GalEye = 2
	ELSEIF K1011CarttonFlow % 30 == 5
		K1011GalEye = 1
	ELSE
		K1011GalEye = 0
	ENDIF
	;翅膀
	IF K1011GalMood == "Happy"
		;会被对话框挡住,扑腾翅膀做废
		K1011GalWing = 0
	ELSE
		K1011GalWing = 0
	ENDIF

;INPUT
@k1011GalPInput()
	#DIM height
	#DIM index
	#DIM char
	height = k1011GetHeight()
	;追加问题
	FOR index, 0, 4
		SIF K1011GalChoiceList:index == ""
			BREAK
		K1011GalText += @"\n[ {index}]"
		K1011GalTextColor += @"{GETDEFCOLOR()}_" * 5
		K1011GalText += K1011GalChoiceList:index
		K1011GalTextColor += @"{GETDEFCOLOR()}_" * STRLENSU(K1011GalChoiceList:index)
	NEXT
	;上面清空
	PRINTV "\n" * (height - 27)
	;显示
	CALL k1011PrintShow()
	WHILE 1
		INPUT
		IF RESULT >= 0 && RESULT < 4 && K1011GalChoiceList:RESULT != ""
			RETURN RESULT
		ELSE
			CLEARLINE 1
		ENDIF
	WEND
