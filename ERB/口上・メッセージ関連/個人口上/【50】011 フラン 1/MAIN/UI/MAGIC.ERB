@k1011MagicUi
	#DIM alpha
	#DIM DYNAMIC mod
	#DIM step
	#DIM flushTime
	#DIM line
	#DIM height
	#DIM printLine
	#DIM DYNAMIC infoId = -1
	#DIM placeId
	#DIM magicIndexPlace, 100
	#DIM magicIndexPlaceIndex
	#DIM index
	#DIM lineNum
	#DIM nowLineNum
	#DIM choice
	#DIM DYNAMIC useMagicFlag
	#DIM DYNAMIC magicMod = 7
	#DIMS DYNAMIC magicType, 100
	#DIMS DYNAMIC magicName, 100
	#DIMS DYNAMIC magicInfo, 100
	#DIM DYNAMIC magicCost, 100
	#DIM DYNAMIC magicLCost, 100
	#DIM DYNAMIC magicId, 100
	#DIM DYNAMIC magicIndex
	#DIM DYNAMIC magicState, 100 ;0默认 1本次设置打开 2进入时打开
	#DIM out
	#DIM DYNAMIC magicChangeNum	;魔法改动的数量,tw算时间用
	;redraw禁用
	REDRAW 0
	;读取数据
	CALL k1011MagicData, magicType, magicName, magicInfo, magicCost, magicLCost, magicId, magicIndex
	flushTime = 200
	lineNum = (CLIENTWIDTH() / GETCONFIG("フォントサイズ") - 1) * 2 / 30
	line = LINECOUNT
	CALLF k1011MpManageInit
	;记录那个是已经打开的
	FOR index, 0, 100
		IF magicType:index == "永续" && K1011MFlag:(magicId:index)
			magicState:index = 2
		ENDIF
	NEXT
	CALL k1011MagicBar, "init"
	WHILE 1
		nowLineNum = 0
		printLine = 0
		height = CLIENTHEIGHT() / GETCONFIG("一行の高さ")
		VARSET magicIndexPlace, -1
		magicIndexPlaceIndex = 0
		CALLF k1011DynamicColor("step")
		IF height < 16
			PRINTS ("窗体高度不足,请将窗体拉高" + UNICODE(10)) * MAX((height - 2), 0)
		ELSE
			DRAWLINE
			PRINTL   ＞ 要让芙兰酱施展怎样的魔法呢?
			DRAWLINE
			ALIGNMENT CENTER
			PRINTL
			CALL k1011MagicBar
			PRINTL
			CUSTOMDRAWLINE ･
			IF magicMod & 1
				SETCOLOR k1011DynamicColor("origin")
				PRINTBUTTON "[即时]", 101
			ELSE
				SETCOLOR 0x747474
				PRINTBUTTON "[即时]", 101
			ENDIF
			RESETCOLOR
			PRINT     
			IF magicMod & 2
				SETCOLOR k1011DynamicColor("blue")
				PRINTBUTTON "[永续]", 102
			ELSE
				SETCOLOR 0x747474
				PRINTBUTTON "[永续]", 102
			ENDIF
			RESETCOLOR
			PRINT     
			IF magicMod & 4
				SETCOLOR k1011DynamicColor("purple")
				PRINTBUTTON "[不定]", 104
			ELSE
				SETCOLOR 0x747474
				PRINTBUTTON "[不定]", 104
			ENDIF
			RESETCOLOR
			PRINTL
			CUSTOMDRAWLINE ･
			ALIGNMENT LEFT
			PRINT   
			nowLineNum = lineNum
			;[即时]打印
			IF magicMod & 1
				FOR index, 0, 100
					IF magicType:index == "即时"
						;换行
						IF nowLineNum == 0
							nowLineNum = lineNum
							printLine ++
							PRINTL
							PRINT   
						ENDIF
						nowLineNum -= 1
						;位置记录
						magicIndexPlace:magicIndexPlaceIndex = index
						magicIndexPlaceIndex ++
						;上色
						IF magicCost:index > k1011CheckMp("now")
							SETCOLOR 0x747474
						ELSE
							SETCOLOR k1011DynamicColor("origin")
						ENDIF
						PRINTBUTTON @"%magicName:index,22,LEFT%", index
						;上色
						IF magicCost:index > k1011CheckMp("now")
							SETCOLOR 0xff2020
						ELSE
							SETCOLOR 0x82e6ff
						ENDIF
						PRINTFORM {magicCost:index,5,RIGHT}
						IF nowLineNum != 0
							SETCOLOR 0x606060
							PRINT  | 
						ENDIF
					ENDIF
				NEXT
			ENDIF
			;[永续]打印
			IF magicMod & 2
				FOR index, 0, 100
					IF magicType:index == "永续"
						;换行
						IF nowLineNum == 0
							nowLineNum = lineNum
							printLine ++
							PRINTL
							PRINT   
						ENDIF
						nowLineNum -= 1
						;位置记录
						magicIndexPlace:magicIndexPlaceIndex = index
						magicIndexPlaceIndex ++
						;上色
						IF K1011MFlag:(magicId:index)
							SETCOLOR k1011DynamicColor("blue")
						ELSEIF magicCost:index > k1011CheckMp("now")
							SETCOLOR 0x747474
						ELSE
							SETCOLOR 0x154cff
						ENDIF
						PRINTBUTTON @"%magicName:index,22,LEFT%", index
						;上色
						;释放ing
						IF K1011MFlag:(magicId:index)
							IF magicCost:index > k1011CheckMp("now")
								SETCOLOR 0xff7474
							ELSE
								SETCOLOR 0x82e6ff
							ENDIF
							PRINTFORM {magicCost:index,2,RIGHT}
							SETCOLOR 0x606060
							PRINT /
							IF magicCost:index > k1011CheckMp("now")
								SETCOLOR 0xff2020
							ELSE
								SETCOLOR 0x00ffee
							ENDIF
							PRINTFORM {magicLCost:index,2,RIGHT}
						;未释放
						ELSE
							IF magicCost:index > k1011CheckMp("now")
								SETCOLOR 0xff2020
							ELSE
								SETCOLOR 0x82e6ff
							ENDIF
							PRINTFORM {magicCost:index,2,RIGHT}
							SETCOLOR 0x606060
							PRINT /
							IF magicCost:index > k1011CheckMp("now")
								SETCOLOR 0xff2020
							ELSE
								SETCOLOR 0x82e6ff
							ENDIF
							PRINTFORM {magicLCost:index,2,RIGHT}
						ENDIF
						IF nowLineNum != 0
							SETCOLOR 0x606060
							PRINT  | 
						ENDIF
					ENDIF
				NEXT
			ENDIF
			;[不定]打印
			IF magicMod & 4
				FOR index, 0, 100
					IF magicType:index == "不定"
						;换行
						IF nowLineNum == 0
							nowLineNum = lineNum
							printLine ++
							PRINTL
							PRINT   
						ENDIF
						nowLineNum -= 1
						;位置记录
						magicIndexPlace:magicIndexPlaceIndex = index
						magicIndexPlaceIndex ++
						;上色
						IF K1011MFlag:(magicId:index)
							SETCOLOR k1011DynamicColor("purple")
						ELSE
							SETCOLOR 0x4800ff
						ENDIF
						PRINTBUTTON @"%magicName:index,27,LEFT%", index
						IF nowLineNum != 0
							SETCOLOR 0x606060
							PRINT  | 
						ENDIF
					ENDIF
				NEXT
			ENDIF
			WHILE height - 15 - printLine
				SIF nowLineNum == lineNum
					PRINT   
				WHILE nowLineNum != 0
					nowLineNum -= 1
					PRINTS " " * 27
					IF nowLineNum != 0
						SETCOLOR 0x606060
						PRINT  | 
					ENDIF
				WEND
				PRINTL
				nowLineNum = lineNum
				printLine ++
			WEND
			RESETCOLOR
			CUSTOMDRAWLINE ･
			infoId = k1011MagicGetInfoId() == -1 ? -1 # magicIndexPlace:k1011MagicGetInfoId()
			IF mod != 1 || infoId == -1 || magicInfo:infoId == ""
				HTML_PRINT "<font color='#747474'>  ※将鼠标移动到一个魔法上来查看说明</font><br><br><br>"
			ELSE
				CALLF k1011MagicInfoNewLine(magicInfo:infoId)
				; WAIT
				HTML_PRINT k1011MagicInfoNewLine(magicInfo:infoId)
			ENDIF
		ENDIF
		DRAWLINE
		PRINT   
		PRINTBUTTON "[998] 返回", 998
		PRINTL
		SELECTCASE mod
			;展开
			CASE 0
				flushTime = 15
				CALL k1011AppearCartoon1, height, 5
				SIF RESULT
					mod = 1
			;等待
			CASE 1
				flushTime = 200
			;退出
			CASE 2
				flushTime = 15
				CALL k1011DisappearCartoon1, height, 5
				SIF RESULT
					BREAK
		ENDSELECT
		IF mod == 0
			TWAIT	flushTime, 0
			RESULT = -1
		ELSE
			TINPUT flushTime, -1, 0
			CLEARLINE 1
		ENDIF
		choice = RESULT
		SELECTCASE choice
			;魔法处理
			CASE 0 TO 99
				SELECTCASE magicType:choice
					CASE "即时"
						IF k1011CheckMp("now") >= magicCost:choice
							PRINTFORML 发动即时魔法ID:{choice}
							CALLFORM k1011Magic{magicId:choice}
							IF RESULT
								SIF k1011MpDown(magicCost:choice) == -1
									THROW [芙兰口上ERROR]MP下降错误
								magicChangeNum += 1
							ENDIF
						ENDIF
					CASE "永续"
						SELECTCASE magicState:choice
							CASE 0
								IF k1011CheckMp("now") >= magicCost:choice
									K1011MFlag:(magicId:choice) = 1
									magicState:choice = 1
									SIF k1011MpDown(magicCost:choice) == -1
										THROW [芙兰口上ERROR]MP下降错误
								ENDIF
							CASE 1
								K1011MFlag:(magicId:choice) = 0
								magicState:choice = 0
								SIF k1011MpUp(magicCost:choice) == -1
									THROW [芙兰口上ERROR]MP增加错误
							CASE 2
								K1011MFlag:(magicId:choice) = !K1011MFlag:(magicId:choice)
						ENDSELECT
					CASE "不定"
						K1011MFlag:(magicId:choice) = !K1011MFlag:(magicId:choice)
				ENDSELECT
			CASE 101, 102, 104
				choice -= 100
				IF magicMod & choice
					magicMod -= choice
				ELSE
					magicMod += choice
				ENDIF
			;返回
			CASE 998
				;永续魔法变动统计
				FOR index, 0, 99
					SIF magicType:index == "永续" && magicState:index == 1
						magicChangeNum += 1
					SIF magicType:index == "永续" && magicState:index == 2 && !K1011MFlag:(magicId:index)
						magicChangeNum += 1
				NEXT
				;tw要改变魔法数量
				IF K1011Game == "TW"
					out = magicChangeNum
				;高难度
				ELSEIF FLAG:3 >= 4
					IF magicChangeNum
						out = 1
					ELSE
						out = 0
					ENDIF
				;低难度
				ELSE
					out = 0
				ENDIF
				mod = 2
		ENDSELECT
		CLEARLINE LINECOUNT - line
	WEND
	;应用mp
	CALLF k1011MpApply
	;redraw启用
	REDRAW 0
	RETURN out

;动态颜色
@k1011DynamicColor(mod)
	#FUNCTION
	#DIMS mod
	#DIM step
	#DIM upFlag
	SELECTCASE mod
		CASE "step"
			;渐变色渲染
			IF upFlag
				step += 10
			ELSE
				step -= 10
			ENDIF
			SIF step >= 100
				upFlag = 0
			SIF step <= 0
				upFlag = 1
		CASE "blue"
			RETURNF color_mix(0x82e6ff, step, 0x00cdff, 100 - step)
		CASE "origin"
			RETURNF color_mix(0xffb982, step, 0xff7000, 100 - step)
		CASE "purple"
			RETURNF color_mix(0xa96aff, step, 0x6c00ff, 100 - step)
		CASEELSE
			THROW [芙兰口上ERROR]动态颜色错误,传入mod错误,为(%mod%)
	ENDSELECT

;蓝条
@k1011MagicBar, initFlag
	#DIMS initFlag
	#DIM long
	#DIM index
	#DIM crest
	#DIM upFlag
	#DIM normallyLong
	#DIM semenLong
	#DIM colorLong
	#DIM whiteLong
	#DIM deepLong
	#DIM shouldColorLong
	#DIM shouldDeepLong
	#DIM middleVariable
	#DIM needAdd
	long = (CLIENTWIDTH() / 2 / GETCONFIG("フォントサイズ"))
	shouldColorLong = 100 * k1011CheckMp("now") / k1011CheckMp("max")
	shouldDeepLong = 100 - shouldColorLong
	;初始化
	IF initFlag == "init"
		colorLong = shouldColorLong
		deepLong = shouldDeepLong
		semenLong = 100 * k1011SemenMp() / (k1011SemenMp() + K1011MpMax)
		normallyLong = 100 - semenLong
		RETURN
	ENDIF
	;白条计算
	IF shouldColorLong < colorLong
		whiteLong += colorLong - shouldColorLong
		colorLong = shouldColorLong
	ELSE
		;白条减少
		middleVariable = whiteLong / 5
		deepLong += (whiteLong - middleVariable)
		whiteLong = middleVariable
	ENDIF
	;加血
	IF shouldColorLong > colorLong
		deepLong -= MAX(MIN(20, shouldColorLong - colorLong), (shouldColorLong - colorLong) / 3)
		colorLong += MAX(MIN(20, shouldColorLong - colorLong), (shouldColorLong - colorLong) / 3)
	ENDIF
	PRINT MP:
	FOR index, 1, 101
		;上色
		;精液色
		IF index <= semenLong
			;正常色
			IF index <= colorLong
				SETCOLOR color_mix(0xffeeee, index + crest, 0x9b8a8a, 250 - index - crest)
			;白色
			ELSEIF index <= colorLong + whiteLong
				SETCOLOR 0xfffcfa
			;深色
			ELSE
				SETCOLOR 0x7b7b7f
			ENDIF
		;正常色
		ELSE
			;正常色
			IF index <= colorLong
				SETCOLOR color_mix(0x50b0ff, index + crest, 0x2d5dff, 250 - index - crest)
			;白色
			ELSEIF index <= colorLong + whiteLong
				SETCOLOR 0xf0f0ff
			;深色
			ELSE
				SETCOLOR 0x002060
			ENDIF
		ENDIF
		PRINT_RECT long
	NEXT
	;渐变色渲染
	IF upFlag
		crest += 10
	ELSE
		crest -= 10
	ENDIF
	SIF crest >= 150
		upFlag = 0
	SIF crest <= 0
		upFlag = 1

	SETCOLOR 0x00ffee
	PRINTFORM {k1011CheckMp("now"), 4}
	RESETCOLOR
	PRINT /
	IF semenLong
		SETCOLOR 0xffeeee
		PRINTFORM {k1011SemenMp()}
		RESETCOLOR
		PRINT +
	ENDIF
	SETCOLOR 0x2d42ff
	PRINTFORM {K1011MpMax}
	RESETCOLOR
	; WAIT

;得到info的id
@k1011MagicGetInfoId
	#FUNCTION
	#DIM mX
	#DIM mY
	#DIM lineNum
	#DIM height
	#DIM out
	lineNum = (CLIENTWIDTH() / GETCONFIG("フォントサイズ") - 1) * 2 / 30
	height = CLIENTHEIGHT() / GETCONFIG("一行の高さ")
	;得到鼠标x,y
	mX = MOUSEX() / GETCONFIG("フォントサイズ") - 1
	mY = MOUSEY() / GETCONFIG("一行の高さ") + height - 1
	SIF mX < 0
		RETURNF -1
	SIF mY < 9
		RETURNF -1
	out = MIN(mX / 15, lineNum - 1)
	out += (mY - 9) * lineNum
	RETURNF MIN(out, 99)

;介绍换行
@k1011MagicInfoNewLine, in
	#FUNCTIONS
	#DIMS in
	#DIMS DYNAMIC out = "  "
	#DIM index
	#DIM weight
	#DIMS char
	#DIM DYNAMIC nowWeight
	#DIM DYNAMIC backslashFlag
	#DIM DYNAMIC newLineTimes
	#DIM DYNAMIC labelFlag
	DEBUGPRINTFORML 
	DEBUGPRINTFORML %in%
	; in '= STRFORM(in)
	DEBUGPRINTFORML %in%
	weight = (CLIENTWIDTH() / GETCONFIG("フォントサイズ")) * 2 - 2
	nowWeight = weight
	;逐字分析
	FOR index, 0, STRLENSU(in)
		char '= SUBSTRINGU(in, 0, 1)
		in '= SUBSTRINGU(in, 1, -1)
		;标签分析中
		IF labelFlag
			;反斜杠
			IF backslashFlag
				out += char
				backslashFlag = 0
			ELSE
				;结束标志
				SIF char == ">"
					labelFlag = 0
				SIF char == "\\"
					backslashFlag = 1
				out += char
			ENDIF
		;正常文字
		ELSE
			;反斜杠
			IF backslashFlag
				backslashFlag = 0
				nowWeight -= STRLENS(char)
			;通常情况
			ELSE
				;<标签
				IF char == "<"
					labelFlag = 1
				;反斜杠
				ELSEIF char == "\\"
					backslashFlag = 1
				;换行
				ELSEIF char == UNICODE(10)
					nowWeight = 0
					char '= ""
					DEBUGPRINTFORML 发现换行%char%
				;通常
				ELSE
					nowWeight -= STRLENS(char)
				ENDIF
			ENDIF
			out += char
		ENDIF
		;换行
		IF nowWeight <= 0
			out += "<br>  "
			newLineTimes ++
			nowWeight = weight
		ENDIF
		;只显示3行
		SIF newLineTimes >= 3
			BREAK
	NEXT
	WHILE newLineTimes < 2
		out += "<br>  "
		newLineTimes ++
	WEND
	DEBUGPRINTFORML %out%
	RETURNF out

;蓝管理
@k1011MpManageMain, mods, arg1
	#FUNCTION
	#DIMS mods
	#DIM arg1
	#DIM normalMp
	#DIM semenMp
	#DIM maxMp
	#DIM startMp
	#DIM nowMp
	#DIM costMp
	#DIM initFlag
	SIF !initFlag && mods != "init"
		THROW [芙兰口上ERROR]MP管理系统错误,未初始化
	SELECTCASE mods
		;初始化
		CASE "init"
			normalMp = K1011Mp
			semenMp = k1011SemenMp()
			nowMp = K1011Mp + semenMp
			maxMp = semenMp + K1011MpMax
			startMp = nowMp
			initFlag = 1
		;执行结果
		CASE "run"
			initFlag = 0
			costMp = startMp - nowMp
			;exp增加
			K1011MExp += (costMp / 5)
			K1011Mp -= costMp
			costMp = -K1011Mp
			K1011Mp = MAX(0, K1011Mp)
			SIF costMp <= 0
				RETURNF 
			CALLF k1011SemenMpDownF, costMp
		;查询相关
		CASE "now"
			RETURNF nowMp
		CASE "max"
			RETURNF maxMp
		CASE "start"
			RETURNF startMp
		;增减相关
		CASE "up"
			SIF nowMp + arg1 > maxMp
				RETURNF -1
			nowMp += arg1
		CASE "down"
			SIF nowMp - arg1 < 0
				RETURNF -1
			nowMp -= arg1
		CASEELSE
			THROW [芙兰口上ERROR]MP管理系统错误,未知的mods(%mods%)
	ENDSELECT

;初始化
@k1011MpManageInit
	#FUNCTION
	CALLF k1011MpManageMain("init", )

;应用
@k1011MpApply
	#FUNCTION
	CALLF k1011MpManageMain("run", )

;查看mp
@k1011CheckMp, mods
	#FUNCTION
	#DIMS mods
	RETURNF k1011MpManageMain(mods, )

;mp下降
@k1011MpDown, num
	#FUNCTION
	#DIM num
	RETURNF k1011MpManageMain("down", num)

;mp上升
@k1011MpUp, num
	#FUNCTION
	#DIM num
	RETURNF k1011MpManageMain("up", num)

; ;帮助
; @k1011MagicHelp
