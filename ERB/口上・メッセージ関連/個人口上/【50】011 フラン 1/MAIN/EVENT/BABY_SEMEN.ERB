;胎儿精液灌溉
;palam处理时DP
@k1011BabySemenPalamcng
	;限YM
	SIF K1011Game != "YM"
		RETURN
	;开关需要打开
	SIF !K1011Config:semenBaby
		RETURN
	;没怀孕
	SIF !K1011Perg
		RETURN
	K1011BabySemenPalam:精液 = 0
	;被内射时处理
	IF TFLAG:11
		K1011BabySemenPalam:精液 = TFLAG:91
		;胎儿内射进度
		K1011BabySemenJuel:精液 += K1011BabySemenPalam:精液 * K1011BabySemenAbl:精液中毒
		;欲望增加
		SIF K1011BabySemenAbl:精液中毒 > 2
				K1011BabySemenJuel:欲望 += K1011BabySemenPalam:精液 * K1011BabySemenPalam:精液 * K1011BabySemenAbl:欲望
	ENDIF

;大回合结束时DP
@k1011BabySemenTurnend
	#DIM semen
	;限YM
	SIF K1011Game != "YM"
		RETURN
	;开关需要打开
	SIF !K1011Config:semenBaby
		RETURN
	;未进育儿室
	IF K1011Perg == 1
		semen = k1011GetOtherInfo("腔内精液量")
		;肚子中有精液
		IF semen
			;灌精程度增加
			K1011BabySemenJuel:精液 += SQRT(semen) * K1011BabySemenAbl:精液中毒
			;欲望增加
			SIF K1011BabySemenAbl:精液中毒 > 2
				K1011BabySemenJuel:欲望 += semen * K1011BabySemenAbl:欲望
			;升级加点
			;精液中毒
			SIF K1011BabySemenJuel:精液 > PALAMLV:(K1011BabySemenAbl:精液中毒 + 1) && K1011BabySemenAbl:精液中毒 < 5
				K1011BabySemenAbl:精液中毒 += 1
			;欲望
			SIF K1011BabySemenJuel:欲望 > PALAMLV:(K1011BabySemenAbl:欲望 + 1) && K1011BabySemenAbl:欲望 < 5
				K1011BabySemenAbl:欲望 += 1
		;没有
		ELSE
			;灌精程度下降
			K1011BabySemenJuel:精液 -= RAND:5
		ENDIF
	;没怀孕
	ELSE
		K1011BabySemenJuel:精液 = 0
		K1011BabySemenAbl:精液中毒 = 0
		K1011BabySemenJuel:欲望 = 0
		K1011BabySemenAbl:欲望 = 0
	ENDIF

;孩子独立
@k1011BabySemenIndepence
	#DIM index
	#DIM targetId
	;限YM
	SIF K1011Game != "YM"
		RETURN
	;开关需要打开
	SIF !K1011Config:semenBaby
		RETURN
	;打印文字
	IF K1011BabySemenAbl:精液中毒
		PRINTFORMW 由于%CALLNAME:MASTER%与芙兰怀孕时的特殊运动、在精液里长大的孩子对精液产生了依赖
	ENDIF
	FOR index, 0, CFLAG:3011
		FINDCHARA CALLNAME, CSTR:(21 + index)
		IF RESULT != -1
			targetId = RESULT
			IF K1011BabySemenAbl:精液中毒
				CALL k1011AddAbl("精液中毒", K1011BabySemenAbl:精液中毒, targetId)
				PRINTFORMW %CSTR:(21 + index)%的精液中毒达到了{K1011BabySemenAbl:精液中毒}
			ENDIF
			IF K1011BabySemenAbl:欲望
				CALL k1011AddAbl("欲望", K1011BabySemenAbl:欲望, targetId)
				PRINTFORMW %CSTR:(21 + index)%的欲望达到了{K1011BabySemenAbl:欲望}
			ENDIF
			IF K1011BabySemenAbl:精液中毒 > 3
				CALL k1011SetTalent("精爱味觉", 1, targetId)
				PRINTFORMW %CSTR:(21 + index)%获得了精爱味觉
			ENDIF
		ENDIF
	NEXT
	K1011BabySemenJuel:精液 = 0
	K1011BabySemenAbl:精液中毒 = 0
	K1011BabySemenJuel:欲望 = 0
	K1011BabySemenAbl:欲望 = 0
