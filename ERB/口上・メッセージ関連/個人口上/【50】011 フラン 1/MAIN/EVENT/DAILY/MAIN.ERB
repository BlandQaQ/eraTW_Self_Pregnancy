;========================================================
;日常事件
;会涉及专用事件触发
;========================================================
@k1011DailyEventMain
	;启用日常事件
	IF K1011Config:exDaily
		CALL k1011DailyFirst
		SIF RESULT
			RETURN
		CALL k1011DailyHeight
		SIF RESULT
			RETURN
		CALL k1011DailyMiddle
		SIF RESULT
			RETURN
		CALL k1011DailyLow
		SIF RESULT
			RETURN
	ELSE
		;通用事件
		TRYCALL k1011NormallyDayEvent
	ENDIF

;夜间事件
@k1011NightEventMain
	;玩家变吸血鬼
	IF k1011GetExp("吸血经验", MASTER) >= 74 && !k1011IsVampirism(MASTER) && k1011GetTalent("亲爱")
		CALL k1011MasterToVampirism()
	;夜间事件
	ELSE
		;启用日常事件
		IF K1011Config:exDaily
			CALL k1011NightLow
		ELSE
			TRYCALL k1011NormallyNightEvent()
		ENDIF
	ENDIF


;超高优先级事件
@k1011DailyFirst

;高优先级事件
@k1011DailyHeight
	;-------------------------------------
	;节日
	;芙兰日
	IF k1011GetDay("芙兰日") && !K1011Festive:event:芙兰日
		CALL k1011DFBirthdayFlan
		K1011Festive:event:芙兰日 = 1
		RETURN 1
	;玩家生日
	ELSEIF k1011GetDay("玩家生日") && k1011GetTalent("恋慕") && !K1011Festive:event:玩家生日
		CALL k1011DFMasterBirthday
		K1011Festive:event:玩家生日 = 1
		RETURN 1
	; ;情人节
	; ;6.1
	ENDIF
	RETURN

;中优先级事件
@k1011DailyMiddle
	#DIMS DYNAMIC eventNameList, 100
	#DIM DYNAMIC eventWeightList, 100
	#DIM DYNAMIC index

	;见面纪念日
	IF k1011GetTalent("恋慕") && k1011GetCflag("好感") > 6000 && !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印") && k1011GetDay("见面日")
		eventNameList:index = k1011DEventMeetDay
		eventWeightList:index = 100
		index += 1
	ENDIF
	;没有事件
	SIF index == 0
		RETURN
	;调用事件
	CALLFORM %eventNameList:k1011WRand(eventWeightList)%
	RETURN 1

;低优先级事件
@k1011DailyLow
	#DIMS DYNAMIC eventNameList, 100
	#DIM DYNAMIC eventWeightList, 100
	#DIM DYNAMIC index
	;TODO催眠师(未写)
	;-------------------------------------
	;日常事件
	;电子游戏事件
	IF K1011DEventFlow:车万游戏 < 1
		eventNameList:index = k1011DTohoGame
		eventWeightList:index = 10
		index += 1
	ENDIF
	;弹幕游戏事件
	IF k1011GetTalent("恋慕") && !K1011DEventFlow:弹幕游戏
		eventNameList:index = k1011DStgGame
		eventWeightList:index = 10
		index += 1
	ENDIF
	;客人事件
	IF (k1011GetTalent("亲爱") || k1011GetTalent("相爱")) && k1011GetAbl("烹饪技能") >= 4 && k1011GetTalent("精爱味觉") && (K1011Game == "TW" || k1011GetCflag("食物掺入物") == 1)
		eventNameList:index = k1011DCustomComeSemen
		eventWeightList:index = 10
		index += 1
	ENDIF
	;魔法发动起始事件
	IF !K1011AllowMagic && k1011GetTalent("恋慕") && !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印") && (!k1011GetTalent("服从") || k1011GetTalent("妄信"))
		eventNameList:index = k1011DEventMagicGet
		eventWeightList:index = 10
		index += 1
	ENDIF
	;看星星事件
	IF K1011_IS_NIGHT && !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印")
		eventNameList:index = k1011DSeeStar
		eventWeightList:index = 10
		index += 1
	ENDIF
	;刷牙
	IF K1011_IS_NIGHT && !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印") && k1011GetTalent("恋慕")
		eventNameList:index = k1011DBrushTeeth
		eventWeightList:index = 10
		index += 1
	ENDIF
	;搓麻将
	IF K1011_IS_NIGHT && !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印") && k1011GetTalent("恋慕")
		eventNameList:index = k1011DMahjong
		eventWeightList:index = 10
		index += 1
	ENDIF
	;妖怪山
	IF !k1011GetMark("反发刻印") && !k1011GetMark("恐怖刻印") && k1011GetTalent("恋慕")
		eventNameList:index = k1011DMonsterMountain
		eventWeightList:index = 10
		index += 1
	ENDIF
	;下午茶
	IF (k1011GetTalent("精爱味觉") && k1011GetTalent("淫乱")) || K1011AllowVampirism
		eventNameList:index = k1011DTea
		eventWeightList:index = 10
		index += 1
	ENDIF
	;去除拆家
	IF k1011GetTalent("恋慕") && !K1011PreventDestroy
		eventNameList:index = k1011DCalm
		eventWeightList:index = 100
		index += 1
	ENDIF
	;怀孕,tw特供
	IF k1011GetTalent("恋慕") && k1011GetTalent("育儿中") && k1011GetOtherInfo("父亲") == k1011RealNo(NO:MASTER)
		eventNameList:index = k1011CheckBaby
		eventWeightList:index = 10
		index += 1
	ENDIF
	;通用事件
	IF K1011Game == "YM"
		eventNameList:index = k1011NormallyDayEvent
		TRYCALL k1011GetNorallyDayilyWeight
		eventWeightList:index = RESULT
		index += 1
	ENDIF

	;没有事件
	SIF index == 0
		RETURN
	;调用事件
	CALLFORM %eventNameList:k1011WRand(eventWeightList)%
	RETURN 1

;低概率夜间事件
@k1011NightLow
	#DIMS DYNAMIC eventNameList, 100
	#DIM DYNAMIC eventWeightList, 100
	#DIM DYNAMIC index
	;肉棒过家家
	IF k1011GetTalent("性别") == 3 && k1011GetTalent("恋慕")
		eventNameList:index = k1011NHPlayHouse
		eventWeightList:index = 10
		index += 1
	ENDIF
	;通用事件
	eventNameList:index = k1011NormallyNightEvent
	eventWeightList:index = 400
	index += 1

	;调用
	CALLFORM %eventNameList:k1011WRand(eventWeightList)%
