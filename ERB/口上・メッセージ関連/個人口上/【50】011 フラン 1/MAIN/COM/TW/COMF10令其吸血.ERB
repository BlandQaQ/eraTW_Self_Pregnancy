;10令其吸血
@k1011ComAbl10
	;芙兰要饿了才行
	SIF !TIME_PROGRESS(TCVAR:空腹時刻)
		RETURN 0
	;你先生的体力
	SIF BASE:MASTER:0 <= 1000
		RETURN 0
	;时停
	SIF FLAG:70
		RETURN 0
	RETURN 1

@k1011Com10
	;加体力
	CALL RECOVER_PERMIL(TARGET, 300, "体力", 0, "吸血的回復")
	CALL RECOVER_PERMIL(TARGET, 200, "気力", 0, "吸血的回復")
	;心情变好
	IF TALENT:心情 < 0
		CALL KIGEN_CHANGE(TARGET, 100, 1)
	ELSE
		CALL KIGEN_CHANGE(TARGET, 50, 1)
	ENDIF
	TFLAG:193 = 1
	;你先生扣点数
	DOWNBASE:MASTER:0 += 495
	DOWNBASE:MASTER:1 += 495

	;饱腹度处理
	TCVAR:TARGET:空腹時刻 = 1440 * DAY + TIME + 360

	;SOURCE处理
	SOURCE:歓楽 = 500

	;ABL:従順
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 200 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 750 + (ABL:従順 * 75)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
	ENDIF

	;好感度影响
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0


	SOURCE:受動 = 200 + 100 * ABL:従順
	SOURCE:征服 = 200 + 100 * ABL:施虐属性

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
	ENDIF

	TFLAG:情緒上昇抑制 = 1
	;exp
	CALL k1011AddExp("吸血经验", 1, MASTER)

	;信頼
	TFLAG:98 += 10

	TIME += 10
	RETURN 1
