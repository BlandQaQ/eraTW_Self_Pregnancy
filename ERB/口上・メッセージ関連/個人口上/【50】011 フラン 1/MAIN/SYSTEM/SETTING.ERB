;========================================================
;设置
;========================================================
	;--------------------------------------------------
	;角色设置
	;--------------------------------------------------
	@k1011CharaSetting
		#DIM line_1
		#DIM INDEX
		;二周目
		IF K1011Times
			IF k1011Love()
				line_1 = LINECOUNT
				FOR INDEX, 0, 50
					PRINTL
				NEXT
				PRINTFORML 你对芙兰的爱……
				PRINTL [0] 没以前那么……喜欢了
				PRINTL [1] 是不会改变的
				WHILE 1
					INPUT
					IF RESULT == 0
						K1011LoveFlan = 0
						BREAK
					ELSEIF RESULT == 1
						K1011LoveFlan = 1
						BREAK
					ELSE
						REUSELASTLINE 二选一没那么难吧
					ENDIF
				WEND
				CLEARLINE LINECOUNT - line_1
			ENDIF
			PRINTFORML
			PRINTFORML
			PRINTFORML
			PRINTFORML 你的芙兰朵露是……
			PRINTFORML 
			PRINTL [0] 和上次一样的性格
			PRINTL [1] 想换换胃口了
			WHILE 1
				INPUT
				IF RESULT == 0
					;对性知识的了解
					;二周目统一这个值,你都玩过了
					K1011InfOfSex = 2
					BREAK
				ELSEIF RESULT == 1
					GOTO MEET_SETTING
					BREAK
				ENDIF
			WEND
		ELSE
			;询问是否喜欢芙兰
			CALL k1011AskLove()
			$MEET_SETTING
			PRINTFORML
			PRINTFORML
			PRINTFORML 
			PRINTFORML 你的芙兰朵露是……
			PRINTFORML 
			PRINTL [0] 淘气的孩子
			PRINTL [1] 有点早熟的孩子
			$INPUT_LOOP_1
			INPUT
			IF RESULT == 0
				PRINTFORML ―芙兰进入天真烂漫模式―
				IF k1011GetTalent("问答态度") == 1 || k1011GetTalent("小恶魔")
					PRINTFORMW ［傲气］［小恶魔］消去
				ENDIF
				IF k1011GetTalent("自尊心") == 1 || k1011GetTalent("冷漠")
					PRINTFORMW ［自尊心高］［冷漠］消去
				ENDIF
				CALL k1011SetTalent("问答态度", 0)
				CALL k1011SetTalent("小恶魔", 0)
				CALL k1011SetTalent("自尊心", 0)
				CALL k1011SetTalent("冷漠", 0)
				;性格
				K1011Character = 1
			ELSEIF RESULT == 1
				PRINTFORML ―芙兰进入有点早熟模式―［傲气］［小恶魔］
				CALL k1011SetTalent("问答态度", 1)
				CALL k1011SetTalent("小恶魔", 1)
				IF k1011GetTalent("自尊心") == 1 || k1011GetTalent("冷漠")
					PRINTFORMW ［自尊心高］［冷漠］消去
				ENDIF
				CALL k1011SetTalent("自尊心", 0)
				CALL k1011SetTalent("冷漠", 0)
				;性格
				K1011Character = 2
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
			WAIT
			;只有在没调教过的状态下有用
			IF k1011GetExp("主人调教经验") == 0
				PRINTL 　
				PRINTL 芙兰对于色色的事情――
				PRINTL 　
				PRINTL 当然、也不是什麼都不知道――
				PRINTL [0] 「什麼东西？ 那是什麼？」
				PRINTL 因为稍微有点兴趣啦、其实房间平时――
				PRINTL [1] 「只是一点点…只是有一点点哦…」
				;超级瑟瑟模式
				PRINTL [2] 「因为在房间里闲着不做些什麼的话脑袋会变得笨笨的呦」
				$INPUT_LOOP_2
				INPUT
				;CFLAG7801を使用
				IF RESULT == 0
					PRINTFORML ―芙兰进入什麼不都知道的模式―
					CALLF k1011SetExp("痛苦快乐经验", 0)
					CALLF k1011SetExp("自慰经验", 0)
					CALLF k1011SetAbl("欲望", 0)
					CALL k1011SetTalent("性好奇", 0)
					CALL k1011SetTalent("快感接受度", 0)
					K1011InfOfSex = 1
				ELSEIF RESULT == 1
					PRINTFORML ―芙兰进入色色的孩子的模式―
					CALL k1011SetTalent("快感接受度", 0)
					K1011InfOfSex = 2
				ELSEIF RESULT == 2
					PRINTFORML ―芙兰进入非常色色的孩子的模式 呜  哼	哼
					;495次、直接自慰中毒max
					CALLF k1011SetExp("自慰经验", 80)
					CALLF k1011SetExp("Ａ经验", 1)
					CALL k1011SetTalent("容易自慰", 1)
					K1011InfOfSex = 3
				ELSE
					GOTO INPUT_LOOP_2
				ENDIF
				WAIT
			ELSE
				;２週目の場合、適当にフラグを入れとく
				K1011InfOfSex = 2
			ENDIF
			;--------------------------------------------------
			PRINTL 　
			PRINTL 咲夜做过红豆饭了没有？ （译注：日本有在家里女孩子来初潮的那一天做红豆饭的习俗） 
			PRINTL [0] 「还没做」
			PRINTL [1] 「已经做过了」
			SIF K1011Times
				PRINTL [2] 「处女情况是不可能变的」
			$INPUT_LOOP_3
			INPUT
			IF RESULT == 0
				PRINTFORML ―芙兰酱、现在还不能怀孕噢―
				PRINTFORML ※调教中、说不定可能哪天就会来初潮
				K1011NotMenchare = 1
				CALL k1011SetOtherInfo("未经初潮", 1)
				;3669是初潮数值。-2是初潮前，-1是初潮(只调教一次)，0是月经后
			ELSEIF RESULT == 1
				PRINTFORML ―芙兰酱进入可以生下小宝宝的模式―
				CALL k1011SetOtherInfo("未经初潮", 0)
				K1011NotMenchare = 0
			ELSEIF RESULT == 2
				PRINTFORML ―芙兰继承了上周末的处女情况―
			ELSE
				GOTO INPUT_LOOP_3
			ENDIF
			WAIT
			;--------------------------------------------------
			;结局分支
			PRINTL 　
			PRINTL 还有、芙兰她――――
			PRINTL [0] 有种哪里坏掉了的感觉
			PRINTL [1] 一直在心里想着总有一天要成为大人
			SIF K1011Times
				PRINTL [2] 和以往一样
			$INPUT_LOOP_5
			INPUT
			IF RESULT == 0
				CALL k1011SetTalent("疯狂", 1)
				K1011Future = 0
			ELSEIF RESULT == 1
				K1011Future = 1
			ELSEIF RESULT == 2
			ELSE
				GOTO INPUT_LOOP_5
			ENDIF
			WAIT
			PRINTL 　
			PRINTL 最后、芙兰――――
			PRINTL [0] 十分乖巧听话(简单)
			PRINTL [1] 肯定有自己的个性(默认)
			PRINTL [2] 不会太听话吧(困难)
			PRINTFORML [3] 会把%CALLNAME:MASTER%撕成两半(地狱)
			PRINTL [4] 我是来受折磨的(噩梦)
			$DIFFICULT_CHOICE
			WHILE 1
				INPUT
				IF RESULT == 0
					PRINTFORML ―芙兰进入了听话模式―
					PRINTFORML 	――不会因为関起来而大吼大叫了
					CALL k1011SetTalent("快感接受度", 1)
					;难度
					K1011Difficult = 0
					BREAK
				ELSEIF RESULT == 1
					PRINTFORML ―芙兰进入了默认模式―
					PRINTFORML 	――这个模式并不简单
					;难度
					K1011Difficult = 1
					BREAK
				ELSEIF RESULT == 2
					PRINTFORML ―芙兰进入了棘手模式―
					PRINTFORML 	――芙兰可不是听话的乖孩子
					;难度
					K1011Difficult = 2
					BREAK
				ELSEIF RESULT == 3
					PRINTFORML ―芙兰进入了地狱模式―
					PRINTFORML 	――看来你很享受调教的过程
					;难度
					K1011Difficult = 3
					BREAK
				ELSEIF RESULT == 4
					PRINTFORML ―你进入了噩梦模式―
					PRINTFORML 	――为什麼是你呢、因为作者已经不考虑人设了
					;难度
					K1011Difficult = 4
					BREAK
				ENDIF
			WEND
			WAIT
			PRINTL 
			PRINTL 
			WAIT
		ENDIF
		;不同难度素质设定
		CALL k1011DifficultTalentSet
		K1011Times += 1

	;--------------------------------------------------
	;不同难度下素质设定
	;--------------------------------------------------
	@k1011DifficultTalentSet
		IF K1011Difficult == 0
			CALL k1011SetTalent("快感接受度", 1)
		ELSEIF K1011Difficult > 1
			CALL k1011SetTalent("快感接受度", 0)
		ENDIF
		IF K1011Difficult > 1
			CALL k1011SetTalent("问答态度", 1)
			CALL k1011SetTalent("冷漠", 1)
			CALL k1011SetTalent("自尊心", 1)
			CALL k1011SetTalent("小恶魔", 0)
			CALL k1011SetTalent("性好奇", 0)
		ENDIF
		IF K1011Difficult == 4
			CALL k1011SetTalent("讨厌人", 3)
			CALL k1011SetTalent("药毒耐性", 1)
			CALL k1011SetTalent("拒绝", 1)
		ELSE
			CALL k1011SetTalent("讨厌人", 3)
			CALL k1011SetTalent("药毒耐性", 0)
			CALL k1011SetTalent("拒绝", 0)
		ENDIF

	;-------------------------------------------
	;生日设置
	;6015 月	 6016日
	;-------------------------------------------
	@k1011AskBirthDayMain
		#DIM line
		line = LINECOUNT
		WHILE 1
			IF K1011MasterBirthday:0 == 0
				CALL k1011AskBirthDayMoon
			ELSEIF K1011MasterBirthday:1 == 0
				CALL k1011AskBirthDayDay
				SIF RESULT == -1
					K1011MasterBirthday:0 = 0
			ELSE
				BREAK
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND
	;输入月份
	@k1011AskBirthDayMoon
		PRINTL 
		PRINTFORML 出生日期是那一月呢?
		;ym
		IF K1011Game == "YM"
			PRINTFORML [ 1] [ 2] [ 3] [ 4]
			PRINTFORML [ 5] [ 6] [ 7] [ 8]
			PRINTFORML [ 9] [10] [11] [12]
			WHILE 1
				INPUT
				IF RESULT >= 1 && RESULT <= 12
					K1011MasterBirthday:0 = RESULT
					RETURN
				ENDIF
			WEND
		;tw
		ELSE
			PRINTBUTTON "[春]", 1
			PRINT  
			PRINTBUTTON "[夏]", 2
			PRINT  
			PRINTBUTTON "[秋]", 3
			PRINT  
			PRINTBUTTON "[冬]", 4
			PRINTL
			WHILE 1
				INPUT
				IF RESULT >= 1 && RESULT <= 4
					K1011MasterBirthday:0 = RESULT
					RETURN
				ENDIF
			WEND
		ENDIF
	
	;输入日期
	@k1011AskBirthDayDay
		#DIMS moonName = "", "春", "夏", "秋", "冬"
		;ym
		IF K1011Game == "YM"
			PRINTFORML 是{K1011MasterBirthday:0}月的那一天呢?
			SELECTCASE K1011MasterBirthday:0
				CASE 1,3,5,7,8,10,12
					PRINTFORML [ 1][ 2][ 3][ 4][ 5][ 6][ 7]
					PRINTFORML [ 8][ 9][10][11][12][13][14]
					PRINTFORML [15][16][17][18][19][20][21]
					PRINTFORML [22][23][24][25][26][27][28]
					PRINTFORML [29][30][31]
				CASE 2
					PRINTFORML [ 1][ 2][ 3][ 4][ 5][ 6][ 7]
					PRINTFORML [ 8][ 9][10][11][12][13][14]
					PRINTFORML [15][16][17][18][19][20][21]
					PRINTFORML [22][23][24][25][26][27][28]
					PRINTFORML [29]
				CASE 4,6,9,11
					PRINTFORML [ 1][ 2][ 3][ 4][ 5][ 6][ 7]
					PRINTFORML [ 8][ 9][10][11][12][13][14]
					PRINTFORML [15][16][17][18][19][20][21]
					PRINTFORML [22][23][24][25][26][27][28]
					PRINTFORML [29][30]
			ENDSELECT
			PRINTL [99] 返回
			WHILE 1
				INPUT
				;返回
				SIF RESULT == 99
					RETURN -1
				;异常值处理
				SELECTCASE K1011MasterBirthday:0
					CASE 1,3,5,7,8,10,12
						SIF !(RESULT >= 1 && RESULT <= 31)
							CONTINUE
					CASE 2
						SIF !(RESULT >= 1 && RESULT <= 29)
							CONTINUE
					CASE 4,6,9,11
						SIF !(RESULT >= 1 && RESULT <= 30)
							CONTINUE
				ENDSELECT
				K1011MasterBirthday:1 = RESULT
				CALL k1011AskYn(@"{K1011MasterBirthday:0}月{K1011MasterBirthday:1}是%CALLNAME:MASTER%的生日吗?")
				SIF RESULT
					RETURN 0
			WEND
		;tw
		ELSE
			PRINTFORML 是%moonName:(K1011MasterBirthday:0)%之月的那一天呢?
			PRINTFORML [ 1][ 2][ 3][ 4][ 5][ 6][ 7]
			PRINTFORML [ 8][ 9][10][11][12][13][14]
			PRINTFORML [15][16][17][18][19][20][21]
			PRINTFORML [22][23][24][25][26][27][28]
			PRINTFORML [29][30][31]
			PRINTL [99] 返回
			WHILE 1
				INPUT
				;返回
				SIF RESULT == 99
					RETURN -1
				;异常值处理
				SIF !(RESULT >= 1 && RESULT <= 31)
					CONTINUE
				K1011MasterBirthday:1 = RESULT
				CALL k1011AskYn(@"%moonName:(K1011MasterBirthday:0)%之月{K1011MasterBirthday:1}是%CALLNAME:MASTER%的生日吗?")
				SIF RESULT
					RETURN 0
			WEND
		ENDIF


;========================================================
;UI
;========================================================
	;-------------------------------------------
	;状态显示
	;-------------------------------------------
	@k1011Status
		RESULTS = 
		; RESULTS = 再口上開始时、你没有选择『默认』、一下你选择的参数%UNICODE(10)%
		IF K1011Character == 1
			RESULTS += @" ・天真烂漫模式%UNICODE(10)%"
		ELSEIF K1011Character == 2
			RESULTS += @" ・有点早熟模式%UNICODE(10)%"
		ENDIF
		IF K1011InfOfSex == 1
			RESULTS += @" ・对色情一无所知模式%UNICODE(10)%"
		ELSEIF K1011InfOfSex == 2
			RESULTS += @" ・对h感兴趣的模式%UNICODE(10)%"
		ELSE
			RESULTS += @" ・爱手淫的坏孩子模式%UNICODE(10)%"
		ENDIF
		IF K1011NotMenchare
			RESULTS += @" ・「初潮」尚未到来模式%UNICODE(10)%"
			IF CFLAG:3669 >= -1
				RESULTS += @" 　　　――调教中初潮会到来了、可以怀孕了%UNICODE(10)%"
			ELSE
				RESULTS += @" 　　　――初潮还没有来、怀孕是不可能的%UNICODE(10)%"
			ENDIF
		ELSE
			RESULTS += @" ・「初潮」已经到来的模式%UNICODE(10)%"
		ENDIF
		IF K1011Future == 1
			RESULTS += @" ・ %CALLNAME:MASTER%想让芙兰在某一天成为真正的女人%UNICODE(10)%"
		ELSE
			RESULTS += @" ・ %CALLNAME:MASTER%很喜欢有种坏掉的感觉的芙兰%UNICODE(10)%"
		ENDIF
		;8013 难度  0简单(无原版发疯事件) 1默认 2困难(原本困难) 3地狱(剧情杀) 4噩梦(7760)
		; SELECTCASE K1011Difficult
		;	 CASE 0
		;		 PRINTFORM ・ 芙兰是个十分乖巧听话的孩子(
		;		 ;绿色
		;		 SETCOLOR 0x44FF00
		;		 PRINT 简单
		;		 RESETCOLOR
		;		 PRINTL )
		;	 CASE 1
		;		 PRINTFORM ・ 芙兰是个偶尔会捣乱的孩子(
		;		 ;黄色
		;		 SETCOLOR 0xFFFF00
		;		 PRINT 普通
		;		 RESETCOLOR
		;		 PRINTL )
		;	 CASE 2
		;		 PRINTFORM ・ 芙兰是个不听话的孩子(
		;		 ;橙色
		;		 SETCOLOR 0xFFBC00
		;		 PRINT 困难
		;		 RESETCOLOR
		;		 PRINTL )
		;	 CASE 3
		;		 PRINTFORM ・ 芙兰喜欢把%CALLNAME:MASTER%撕成两半(
		;		 ;红色
		;		 SETCOLOR 0xFF4400
		;		 PRINT 地狱
		;		 RESETCOLOR
		;		 PRINTL )
		;	 CASE 4
		;		 PRINTFORM ・ 你是来受折磨的模式(
		;		 ;紫色
		;		 SETCOLOR 0xFF00CD
		;		 PRINT 噩梦
		;		 RESETCOLOR
		;		 PRINTL )
		; ENDSELECT
		SELECTCASE K1011Difficult
			CASE 0
				RESULTS += @" ・ 芙兰是个十分乖巧听话的孩子(简单)"
			CASE 1
				RESULTS += @" ・ 芙兰是个偶尔会捣乱的孩子(普通)"
			CASE 2
				RESULTS += @" ・ 芙兰是个不听话的孩子(困难)"
			CASE 3
				RESULTS += @" ・ 芙兰喜欢把%CALLNAME:MASTER%撕成两半(地狱)"
			CASE 4
				RESULTS += @" ・ 你是来受折磨的模式(噩梦)"
		ENDSELECT
		RETURN 0

	;[ 0]基本设置
	@k1011Setting0
		#DIM line
		;删除行数
		line = LINECOUNT
		WHILE 1
			DRAWLINE
			PRINTL 基本设置
			DRAWLINE
			PRINTFORML \@(K1011Config:defenceNtr) ? √ # × \@[ 0] 防NTR開関
			PRINTFORML \@(K1011Config:wordPrint) ? √ # × \@[ 2] 对话逐字打印
			PRINTFORML \@(K1011Config:randSound) ? √ # × \@[ 4] 随机声音
			PRINTL
			PRINT [50] 交互模式:
			SELECTCASE K1011Config:printMod
				CASE 0
					PRINTL 普通
				CASE 1
					PRINTL 字符画仿GAL
				CASE 2
					PRINTL GAL
			ENDSELECT
			PRINTFORML	[98] 说明
			PRINTFORML	[99] 返回
			INPUT
			IF RESULT == 0
				K1011Config:defenceNtr = !K1011Config:defenceNtr
			ELSEIF RESULT == 2
				K1011Config:wordPrint = !K1011Config:wordPrint
			ELSEIF RESULT == 4
				K1011Config:randSound = !K1011Config:randSound
			ELSEIF RESULT == 50
				K1011Config:printMod ++
				SIF K1011Config:printMod > 2
					K1011Config:printMod = 0
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 基本设置说明
				DRAWLINE
				PRINTL [ 0] 防NTR開関
				PRINTL 	如果你不喜欢芙兰或者喜欢NTR请関掉
				PRINTL 	这个东西会在游戏中NTR文本显示前让你读档
				PRINTL 	会导致无法售卖、卖淫等问题
				PRINTL
				PRINTL [ 2] 对话逐字打印
				PRINTL 	对话时的文字会像gal一样逐字打印、按任意键跳过该句打印
				PRINTL 	注意:播放时过程中、右键为跳过该句对话、不能进入进入跳过状态、结束播放时才能进入跳过状态
				PRINTL
				PRINTL [ 3] 随机声音
				PRINTL 	随机生成一些教程、高潮等声音
				PRINTL 	质量不如原本的文本、但是当你看逆了原本的文本、打开这个是个不错的选择
				PRINTL
				PRINTL [50] 交互模式
				PRINTL 	普通:常规的口上交互方式
				PRINTL 	字符画仿GAL:试试就知道了(测试内容,存在大量瑕疵,慎重)
				PRINTL 	GAL:试试就知道了(测试内容,存在大量瑕疵,慎重)
				PRINTW
			ELSEIF RESULT == 99
				CLEARLINE LINECOUNT - line
				BREAK
			ELSE
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND
	;[ 1]内容设置
	@k1011Setting1
		#DIM line, 2
		;删除行数
		line = LINECOUNT
		WHILE 1
			DRAWLINE
			PRINTL 内容设置
			DRAWLINE
			PRINTFORML \@ (K1011Config:bDev) ? √ # × \@[ 0] 胸部揉大
			IF K1011Config:bDev
				;删除行数
				PRINT     
				SELECTCASE K1011Config:bDevMax
					CASE 0
						PRINTFORML 壁[10] 最大能揉多大
					CASE 1
						PRINTFORML 贫[10] 最大能揉多大
					CASE 2
						PRINTFORML 普[10] 最大能揉多大
					CASE 3
						PRINTFORML 巨[10] 最大能揉多大
					CASE 4
						PRINTFORML 爆[10] 最大能揉多大
				ENDSELECT
			ENDIF
			PRINTFORML \@ (K1011Config:vampirismDie) ? √ # × \@[ 1] 吸血致死
			PRINTFORML \@ (K1011Config:exDaily) ? √ # × \@[ 3] 日常事件
			;日常事件附加设置
			IF K1011Config:exDaily
				; PRINT     
				; PRINTFORML \@ (0) ? √ # × \@[31] 催眠师(未完工)
			ENDIF
			PRINTFORML \@ (K1011Config:combo) ? √ # × \@[ 4] 指令组合
			PRINTFORML \@ (K1011Config:semenBaby) ? √ # × \@[ 5] 胎儿精液灌溉
			PRINTFORML	[98] 说明
			PRINTFORML	[99] 返回
			INPUT
			;揉胸
			IF RESULT == 0
				K1011Config:bDev = !K1011Config:bDev
			;胸揉多大
			ELSEIF RESULT == 10
				K1011Config:bDevMax += 1
				SIF K1011Config:bDevMax >= 5
					K1011Config:bDevMax = 0
			;吸血致死
			ELSEIF RESULT == 1
				K1011Config:vampirismDie = !K1011Config:vampirismDie
			;日常事件
			ELSEIF RESULT == 3
				K1011Config:exDaily = !K1011Config:exDaily
			;指令组合
			ELSEIF RESULT == 4
				K1011Config:combo = !K1011Config:combo
			ELSEIF RESULT == 5
				K1011Config:semenBaby = !K1011Config:semenBaby
			;说明
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 基本设置说明
				DRAWLINE
				PRINTL [ 0] 胸部揉大
				PRINTL 	启用后芙兰的胸部可以揉大
				PRINTL
				PRINTL [10] 最大能揉多大
				PRINTL 	胸最大能揉多大
				PRINTL 	壁-飞机场  贫-贫乳  普-普通乳  巨-巨乳  爆-爆乳
				PRINTL
				PRINTL [ 1] 吸血致死
				PRINTL 	再非吸血鬼模式下、连续吸血两次就会致死
				PRINTL
				PRINTL [ 2] 缠人模式
				PRINTL 	芙兰在早上会缠着你让你、让你直接開始调教
				PRINTL
				PRINTL [ 3] 日常事件
				PRINTL 	会触发口上的日常事件、这是某些事件的前置条件
				PRINTL
				PRINTL [ 4] 指令组合
				PRINTL 	允许将多个指令组合成新指令、如执行过正常位再执行爱抚会组合成正常位爱抚
				PRINTL 	需要和芙兰有比较好的关系才会触发
				PRINTL
				PRINTL [ 5] 胎儿精液灌溉
				PRINTL 	在芙兰怀孕时进行内射会对胎儿产生一定影响(包括日常事件、夜爬事件)
				PRINTL 	胎儿会根据芙兰怀孕时被内射的情况获取精液中毒、欲望、精爱味觉
				PRINTW
			ELSEIF RESULT == 99
				CLEARLINE LINECOUNT - line
				BREAK
			ELSE
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND
	
	;[ 2]显示设置
	@k1011Setting2
		#DIM line
		;删除行数
		line = LINECOUNT
		WHILE 1
			;删除行数
			DRAWLINE
			PRINTL 显示设置
			DRAWLINE
			PRINTFORML \@ (K1011Config:skipAssi) ? √ # × \@[ 0] 助手调教时跳过口上
			PRINTFORML \@ (K1011Config:skipBall) ? √ # × \@[ 1] 口球装备时跳过口上
			PRINTFORML \@ (K1011Config:skipStopTime) ? √ # × \@[ 2] 时间停止时跳过口上
			PRINTFORML \@ (K1011Config:skipPassout) ? √ # × \@[ 2] 失神时跳过口上
			PRINTFORML	[98] 说明
			PRINTFORML	[99] 返回
			INPUT
			IF RESULT == 0
				K1011Config:skipAssi = !K1011Config:skipAssi
			ELSEIF RESULT == 1
				K1011Config:skipBall = !K1011Config:skipBall
			ELSEIF RESULT == 2
				K1011Config:skipStopTime = !K1011Config:skipStopTime
			ELSEIF RESULT == 3
				K1011Config:skipPassout = !K1011Config:skipPassout
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 基本设置说明
				DRAWLINE
				PRINTL [ 0]助手调教时跳过口上(tw玩家无视)
				PRINTL 	设计的有问题、没考虑过助手的情况
				PRINTL
				PRINTL [ 1]口球装备时跳过口上
				PRINTL 	看喜好吧、原作者说带口球应该说不出来话
				PRINTL
				PRINTL [ 2]时间停止时跳过口上
				PRINTL 	时间都停止了、应该说不了话
				PRINTL
				PRINTL [ 3]失神时跳过口上(tw玩家无视)
				PRINTL 	失神时是否显示口上、分为完全显示、部分显示、和不显示
				PRINTW
			ELSEIF RESULT == 99
				CLEARLINE LINECOUNT - line
				BREAK
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND
	
	;[ 3]机制设置
	@k1011Setting3
		#DIM line
		;设置行数设定
		line = LINECOUNT
		WHILE 1
			DRAWLINE
			PRINTL 显示设置
			DRAWLINE
			PRINTFORML \@ (K1011Config:chastity) ? √ # × \@[ 0] 贞操观念重整
			PRINTFORML \@ (K1011Config:stressWithDesire) ? √ # × \@[ 1] 压力越大欲望越大
			PRINTFORML  [98] 说明
			PRINTFORML  [99] 返回
			INPUT
			;贞操观念重整
			IF RESULT == 0
				K1011Config:chastity = !K1011Config:chastity
			;压力越大欲望越大
			ELSEIF RESULT == 1
				K1011Config:stressWithDesire = !K1011Config:stressWithDesire
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 机制设置说明
				DRAWLINE
				PRINTL [ 0]贞操观念重整
				PRINTL 	调整了下贞操观念,调整后更类似tk里的贞操观念
				PRINTL 	详细内容请看口上说明
				PRINTL
				PRINTL [ 1]压力越大欲望越大(tw玩家无视)
				PRINTL 	有压力时欲情追加会增加
				PRINTL 	详细内容请看口上说明
				PRINTW
			ELSEIF RESULT == 99
				CLEARLINE LINECOUNT - line
				RETURN 0
			ELSE
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND
	
	;[90]更改对你的称呼
	@k1011Setting90
		#DIM line
		;删除行数
		line = LINECOUNT
		WHILE 1
			DRAWLINE
			PRINTL 更改对你的称呼
			DRAWLINE
			IF k1011GetTalent("恋慕")
				PRINTFORML [ 0] 芙兰:%k1011Name(TARGET, MASTER)%
			ELSE
				SETCOLOR 0x656565
				PRINTPLAINFORM [ 0] 芙兰:%k1011Name(TARGET, MASTER)%
				PRINTL
				RESETCOLOR
			ENDIF
			PRINTFORML [98] 说明
			PRINTFORML [99] 返回
			INPUT
			IF RESULT == 0
				IF k1011GetTalent("恋慕")
					CALL k1011SelectCallNameFlan
				ELSE
					PRINTL 恋慕(思慕)之会才可以更改哦
				ENDIF
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 更改对你的称呼说明
				DRAWLINE
				PRINTL 只有恋慕(思慕)了才能修改
				PRINTL 只有相爱了才能被称为「达令」
				PRINTW
			ELSEIF RESULT == 99
				CLEARLINE LINECOUNT - line
				BREAK
			ELSE
			ENDIF
			CLEARLINE LINECOUNT - line
		WEND

	;-------------------------------------------
	;设置菜单
	;-------------------------------------------
	@k1011ConfigMenu
		;删除行数
		LOCAL:0 = LINECOUNT
		;恋慕了会多个选项
		WHILE 1
			DRAWLINE
			PRINTL 设置
			DRAWLINE
			PRINTL [ 0] 基本设置
			PRINTL [ 1] 内容设置
			PRINTL [ 2] 显示设置
			PRINTL [ 3] 机制设置
			PRINTL [90] 更改对你的称呼
			PRINTL [98] 说明
			PRINTL [99] 退出
			INPUT
			IF RESULT == 0
				CALL k1011Setting0
			ELSEIF RESULT == 1
				CALL k1011Setting1
			ELSEIF RESULT == 2
				CALL k1011Setting2
			ELSEIF RESULT == 3
				CALL k1011Setting3
			ELSEIF RESULT == 90
				CALL k1011Setting90
			ELSEIF RESULT == 98
				DRAWLINE
				PRINTL 芙兰魔改口上说明
				DRAWLINE
				PRINTL [ 0]基本设置
				PRINTL 	影響口上机制
				PRINTL
				PRINTL [ 1]内容设置
				PRINTL 	影響口上的专用事件(性格选择是「默认」的话都不会启用)
				PRINTL
				PRINTL [ 2]显示设置
				PRINTL 	决定在默写情况下是否显示口上
				PRINTL
				PRINTL [ 3]机制设置
				PRINTL 	一些对原版机制产生影响的设置
				PRINTL
				PRINTL [90]更改对你的称呼
				PRINTL 	只有对该角色恋慕后才能使用
				PRINTW
				CLEARLINE LINECOUNT - LOCAL:0
			ELSEIF RESULT == 99
				DRAWLINE
				PRINTL
				BREAK
			ELSE
			ENDIF
			CLEARLINE LINECOUNT - LOCAL:0
		WEND
	
	;-------------------------------------------
	;设置的主函数
	;-------------------------------------------
	@k1011ConfigMain
		CALL k1011ConfigMenu


;========================================================
;设置组合
;========================================================
@k1011ConfigCombo
	PRINTL
	PRINTL 関于口上设置
	IF K1011Game == "YM"
		PRINTL [0] 推荐组合  给和魔改作者一样、喜欢芙兰、但无法容忍NTR的玩家提供
		PRINTL [1] NTR 组合  关闭了防NTR开关、在享受口上内容时仍可进行售卖、扔妓院等操作
		PRINTL [2] 基本组合  没有特殊事件、选项之类的。方便快速调教完扔妓院、卖钱或者喂触手
		PRINTL [3] 自己设置
	ELSE
		PRINTL [0] 推荐组合  启用了所有功能、推荐玩这个……但如果你玩的是原版tw还是算了
		PRINTL [2] 基本组合  没有很多花里胡哨的功能、理论上稳定性和兼容性是很高的、但缺少的东西比较多
		PRINTL [3] 自己设置
	ENDIF
	;GAL IMAGE自动设定打开
	K1011Config:GIAutoSetting = 1
	WHILE 1
		INPUT
		IF RESULT == 0
			SIF K1011Game == "YM"
				PRINTL 如果想要进行出售、喂给触手、卖淫等行为、请先関掉防NTR開関(基本设置)
			K1011Config:defenceNtr = 1
			K1011Config:skipAssi = 1
			K1011Config:skipBall = 1
			K1011Config:skipStopTime = 1
			K1011Config:skipPassout = 1
			K1011Config:skipStopTime = 1
			K1011Config:bDev = 1
			K1011Config:vampirismDie = 1
			K1011Config:exDaily = 1
			K1011Config:bDevMax = 4
			K1011Config:combo = 1
			K1011Config:chastity = 1
			K1011Config:semenBaby = 1
			K1011Config:wordPrint = 1
			BREAK
		ELSEIF RESULT == 1
			K1011Config:skipAssi = 1
			K1011Config:skipBall = 1
			K1011Config:skipStopTime = 1
			K1011Config:skipPassout = 1
			K1011Config:skipStopTime = 1
			K1011Config:bDev = 1
			K1011Config:vampirismDie = 1
			K1011Config:exDaily = 1
			K1011Config:bDevMax = 4
			K1011Config:combo = 1
			K1011Config:chastity = 1
			K1011Config:semenBaby = 1
			K1011Config:wordPrint = 1
			BREAK
		ELSEIF RESULT == 2
			K1011Config:skipAssi = 1
			K1011Config:skipBall = 1
			K1011Config:skipPassout = 1
			K1011Config:skipStopTime = 1
			K1011Config:bDev = 1
			K1011Config:bDevMax = 4
			BREAK
		ELSEIF RESULT == 3
			K1011Config:skipAssi = 1
			K1011Config:skipBall = 1
			K1011Config:skipStopTime = 1
			K1011Config:skipPassout = 1
			K1011Config:skipStopTime = 1
			K1011Config:bDev = 1
			K1011Config:vampirismDie = 1
			K1011Config:exDaily = 1
			K1011Config:bDevMax = 4
			K1011Config:combo = 1
			K1011Config:chastity = 1
			K1011Config:semenBaby = 1
			K1011Config:wordPrint = 1
			CALL k1011ConfigMenu
			BREAK
		ELSE
			PRINTL 请输入正确的数值
		ENDIF
		CLEARLINE 1
	WEND
	WAIT

@k1011TakeoverSetting
	;询问芙兰性格
	CALL k1011CharaSetting
	;生成描述
	CALL k1011Status
	DRAWLINE
	PRINTFORML %RESULTS%
	DRAWLINE
	IF K1011Game == "YM"
		SIF CSTR:3 == "从奴隶商人处购入。"
			CSTR:3 = 由调教典召唤出来的
		CSTR:3 = %RESULTS%%UNICODE(10)%%CSTR:3%
	ELSE
		CSTR:10 += "\n"
		CSTR:10 += RESULTS
	ENDIF
