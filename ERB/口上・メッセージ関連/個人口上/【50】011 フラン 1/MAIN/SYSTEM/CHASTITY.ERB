;--------------------------------------------------
;贞操观念重做
;--------------------------------------------------
;恋慕前和原版没区别
;恋慕后如果做的对象是你正面buff
;不是你是负面buff


;创建虚拟环境
@k1011ChastityCreateVenv
	;禁用
	SIF !K1011Config:chastity
		RETURN 0
	;不恋慕和原版没区别
	SIF !k1011GetTalent("恋慕")
		RETURN 0
	
	SIF k1011GetTalent("贞操观念") == 1
		K1011HaveChastity = 1
	;贞操观念退让
	CALL k1011SetTalent("贞操观念", 0)

;删虚拟环境
@k1011ChastityDelVenv
	;不恋慕和原版没区别
	SIF !k1011GetTalent("恋慕")
		RETURN 0
	SIF K1011HaveChastity
		CALL k1011SetTalent("贞操观念", 1)

;SOURCE处理
@k1011SChastity
	;不恋慕和原版没区别
	SIF !k1011GetTalent("恋慕")
		RETURN 0
	;没贞操观念不处理
	SIF !K1011HaveChastity
		RETURN 0
	;对象不是玩家
	IF PLAYER != MASTER
		;仅主人插入系指令
		IF GROUPMATCH(k1011GetNowSelectcom(), 20, 21, 22, 23, 24, 27, 28, 612, 613, 614, 615, 616, 617, 630, 632, 30, 31, 32, 33, 34, 37, 38, 618, 619, 633, 610)
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") / 2)
			CALL k1011AddSource("反感追加", 2000)
		;触手系指令
		ELSEIF k1011GetNowSelectcom() >= 500 && k1011GetNowSelectcom() <= 599
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") / 10)
			CALLF k1011SetSource("屈从", k1011GetSource("屈从") * 2 / 5)
			CALL k1011AddSource("反感追加", 5000)
		;兽奸系指令
		ELSEIF k1011GetNowSelectcom() >= 380 && k1011GetNowSelectcom() <= 391
			CALLF k1011SetSource("爱情", 0)
			CALLF k1011SetSource("屈从", k1011GetSource("屈从") / 5)
			CALL k1011AddSource("反感追加", 20000)
		;其他指令不处理
		ELSE
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") * 2 / 3)
			CALL k1011AddSource("反感追加", 500)
		ENDIF
	;对像是玩家
	ELSE
		;仅主人插入系指令
		IF GROUPMATCH(k1011GetNowSelectcom(), 20, 21, 22, 23, 24, 27, 28, 612, 613, 614, 615, 616, 617, 630, 632, 30, 31, 32, 33, 34, 37, 38, 618, 619, 633)
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") * 2)
			CALLF k1011SetSource("反感追加", k1011GetSource("反感追加") / 2)
		;助手插入系指令
		ELSEIF k1011GetNowSelectcom() == 610 && (TFLAG:131 == 1 || TFLAG:131 == 2)
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") * 3 / 5)
			CALL k1011AddSource("反感追加", 1000)
		;触手系指令
		ELSEIF k1011GetNowSelectcom() >= 500 && k1011GetNowSelectcom() <= 599
			;TODO主人触手化
			IF 0
				CALLF k1011SetSource("爱情", k1011GetSource("爱情") * 2)
				CALLF k1011SetSource("反感追加", k1011GetSource("反感追加") / 2)
			ELSE
				CALLF k1011SetSource("爱情", k1011GetSource("爱情") / 2)
				CALLF k1011SetSource("屈从", k1011GetSource("屈从") * 4 / 5)
				CALL k1011AddSource("反感追加", 1000)
			ENDIF
		;兽奸系指令
		ELSEIF k1011GetNowSelectcom() >= 380 && k1011GetNowSelectcom() <= 391
			CALLF k1011SetSource("爱情", 0)
			CALLF k1011SetSource("屈从", k1011GetSource("屈从") / 2)
			CALL k1011AddSource("反感追加", 10000)
		;其他指令效果稍弱
		ELSE
			CALLF k1011SetSource("爱情", k1011GetSource("爱情") * 3 / 2)
			CALLF k1011SetSource("反感追加", k1011GetSource("反感追加") * 2 / 3)
		ENDIF
	ENDIF

;素质获取
@k1011GetTalentChastity
	;没有贞操观念则打印
	SIF !K1011HaveChastity
		PRINTFORMW %CALLNAME:TARGET%获得了[贞操观念]
	CALL k1011SetTalent("贞操观念", 1)

;非项圈
@k1011ChastityNoNecklace
	;禁用
	SIF !K1011Config:chastity
		RETURN 0
	CALL k1011GetTalentChastity

;项圈处理
@k1011Necklace_CHASTITY
	;禁用
	SIF !K1011Config:chastity
		RETURN 0
	PRINTFORML 你觉得芙兰应该是只属于你东西
	PRINTFORML 还是一声令下可以全心全意侍奉他人的奴隶?
	PRINTL [0]她是只属于我的    [1]听话最重要
	WHILE 1
		INPUT
		IF RESULT == 0
			CALL k1011GetTalentChastity
			BREAK
		ELSEIF RESULT == 1
			BREAK
		ENDIF
	WEND

;购买物品时执行
@k1011ChastityItem, ARG
	SIF !K1011Config:chastity
		RETURN
	IF k1011GetTalent("贞操观念")
		SIF ARG
			TCVAR:371 = 1
		K1011HaveChastity = 1
		CALL k1011SetTalent("贞操观念", 0)
		K1011FirstDp = 1p0
	ENDIF

;购买结局物品时执行
@k1011ChastityEndItem
	SIF !K1011Config:chastity
		RETURN
	IF k1011GetTalent("贞操观念")
		K1011HaveChastity = 1
		CALL k1011SetTalent("贞操观念", 0)
	ENDIF

;紧急处理
@k1011ChastityEndFirst
	;项圈
	IF TCVAR:371
		CALL k1011Necklace_CHASTITY
	;非项圈
	ELSE
		CALL k1011ChastityNoNecklace
	ENDIF
