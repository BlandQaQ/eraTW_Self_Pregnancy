;命名空间管理

;命名空间-----------------------------------
;进入口上时命名空间初始化
@k1011nameSpaceInit()
	K1011NameSpace = K1011

;嵌套命名空间
@K1011、addNameSpace(spaceName)
	#DIMS spaceName
	K1011NameSpace += "、"
	K1011NameSpace += spaceName

;消除一层命名空间
@K1011、outNameSpace()
	K1011NameSpace = k1011GetNameSpaceOutDeep(K1011NameSpace)

;函数相关-----------------------------------
;取得函数名
@gf(nameOfFun, nameSpace = "")
	#FUNCTIONS
	#DIMS nameOfFun
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	RETURNF k1011GetVarOrFunctionName(nameOfFun, 1, nameSpace)
;变量相关-----------------------------------
;取得变量名
@gv(nameOfVar, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	RETURNF GETVAR(k1011GetVarOrFunctionName(nameOfVar, 0, nameSpace))

;设置变量值
@sv(nameOfVar, num, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIM num
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	nameOfVar '= k1011GetVarOrFunctionName(nameOfVar, 0, nameSpace)
	SETVAR @"%nameOfVar%", num

;加法
@va(nameOfVar, num, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIM num
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	CALLF sv(gv(nameOfVar, nameSpace), gv(nameOfVar, nameSpace) + num, "")

;乘法
@vm(nameOfVar, num, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIM num
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	CALLF sv(gv(nameOfVar, nameSpace), gv(nameOfVar, nameSpace) * num, "")

;除法
@vd(nameOfVar, num, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIM num
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	CALLF sv(gv(nameOfVar, nameSpace), gv(nameOfVar, nameSpace) / num, "")

;自增
@vpp(nameOfVar, nameSpace = "")
	#FUNCTION
	#DIMS nameSpace
	#DIMS nameOfVar
	#DIM num
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	CALLF sv(gv(nameOfVar, nameSpace), gv(nameOfVar, nameSpace) + 1, "")

;自减
@vdd(nameOfVar, nameSpace = "")
	#FUNCTION
	#DIMS nameOfVar
	#DIMS nameSpace
	;默认命名空间
	SIF nameSpace == ""
		nameSpace '= K1011NameSpace
	CALLF sv(gv(nameOfVar, nameSpace), gv(nameOfVar, nameSpace) - 1, "")

;工具-----------------------------------
;获取外面一层命名空间的名称
@k1011GetNameSpaceOutDeep(nameSpace)
	#FUNCTIONS
	#DIMS nameSpace
	#DIMS DYNAMIC out
	#DIMS tmp, 999
	#DIM spaceDeepth
	#DIM index
	SPLIT nameSpace, "、", tmp
	spaceDeepth = RESULT
	FOR index, 0, spaceDeepth - 1
		;第一个除外
		SIF index
			out += "、"
		out += tmp:index
	NEXT
	DEBUGPRINTFORML spaceDeepth = {spaceDeepth} out = %out%
	RETURNF out

@k1011GetVarOrFunctionName(searchName, isFunction, startNameSpace)
	#FUNCTIONS
	#DIMS searchName
	#DIM isFunction
	#DIMS startNameSpace
	#DIMS nameSpace
	nameSpace '= startNameSpace
	WHILE 1
		;存在
		IF (isFunction && EXISTFUNCTION(@"%nameSpace%、%searchName%")) || (!isFunction && EXISTVAR(@"%nameSpace%、%searchName%"))
			RETURNF @"%nameSpace%、%searchName%"
		ELSE
			;命名空间已经脱完了
			IF nameSpace == ""
				THROW [芙兰口上ERROR]未找到\@ isFunction ? 函数 # 变量\@%searchName%(起始命名空间:%startNameSpace%)
			ELSE
				nameSpace '= k1011GetNameSpaceOutDeep(nameSpace)
			ENDIF
		ENDIF
	WEND
